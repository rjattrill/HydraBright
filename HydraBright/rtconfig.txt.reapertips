;   
;   Reapertips Theme v1.80 (Dark Version)
;   made by Alejandro Hernandez from Reapertips.com
;   
  

#>----------------------------------- GLOBALS -----------------------------------------
version 6.0
use_pngs 1
use_overlays 1
tcp_showborders 0
mcp_showborders 0
transport_showborders 0
tcp_vupeakwidth 2
mcp_vupeakheight 2
mcp_mastervupeakheight 4
mcp_altmeterpos 0
tcp_vol_zeroline FF666666
tcp_pan_zeroline 66000000
mcp_vol_zeroline FF666666
mcp_pan_zeroline 85000000
trans_speed_zeroline 85000000
gen_vol_zeroline FF000000
gen_pan_zeroline FF000000
item_volknobfg 2A2A2A  2A2A2A  2A2A2A
mcp_min_height 240
tcp_master_minheight 56
mcp_voltext_flags 5 5
tcp_voltext_flags 12
tinttcp 298
peaksedges 0
no_meter_reclbl 1
tcp_heights 4 26 61 61   ; supercollapsed, collapsed, small(norecarm), recarm size
tcp_folderindent 0
envcp_min_height 27
misc_dpi_translate 50 100
misc_dpi_translate 134 150
misc_dpi_translate 174 200
adjuster_script "Default_6.0_theme_adjuster.lua"

; ----------------- WALTER -------------------

custom tcp.custom.bg_solid      '' 0 '' 'custom_tcp_bg_solid'
custom tcp.custom.bgsel_solid   '' 0 '' 'custom_tcp_bgsel_solid'

custom tcp.custom.bg_dimmed_1  '' 0 '' 'custom_tcp_bg_dimmed_1'
custom tcp.custom.bg_dimmed_2  '' 0 '' 'custom_tcp_bg_dimmed_2'
custom tcp.custom.bg_dimmed_3  '' 0 '' 'custom_tcp_bg_dimmed_3'
custom tcp.custom.bg_dimmed_4  '' 0 '' 'custom_tcp_bg_dimmed_4'
custom tcp.custom.bg_dimmed_5  '' 0 '' 'custom_tcp_bg_dimmed_5'

custom tcp.custom.bgsel_dimmed_1  '' 0 '' 'custom_tcp_bgsel_dimmed_1'
custom tcp.custom.bgsel_dimmed_2  '' 0 '' 'custom_tcp_bgsel_dimmed_2'
custom tcp.custom.bgsel_dimmed_3  '' 0 '' 'custom_tcp_bgsel_dimmed_3'
custom tcp.custom.bgsel_dimmed_4  '' 0 '' 'custom_tcp_bgsel_dimmed_4'
custom tcp.custom.bgsel_dimmed_5  '' 0 '' 'custom_tcp_bgsel_dimmed_5'

custom tcp.custom.folder_1-1 '' 0 '' 'custom_track_folder_1-1'
custom tcp.custom.folder_1-2 '' 0 '' 'custom_track_folder_1-2'
custom tcp.custom.folder_1-4 '' 0 '' 'custom_track_folder_1-4'
custom tcp.custom.folder_1-8 '' 0 '' 'custom_track_folder_1-8'

custom mcp.custom.folder_1-1 '' 0 '' 'custom_mcp_folder_1-1'
custom mcp.custom.folder_1-2 '' 0 '' 'custom_mcp_folder_1-2'
custom mcp.custom.folder_1-4 '' 0 '' 'custom_mcp_folder_1-4'
custom mcp.custom.folder_1-8 '' 0 '' 'custom_mcp_folder_1-8'

custom mcp.custom.folder_start '' 0 '' 'custom_mcp_folder_start'

custom tcp.custom.fixed_lanes_off '' 42430 'Lanes' 'custom_fixed_lanes_off'
custom tcp.custom.fixed_lanes_on '' 42430 'Lanes' 'custom_fixed_lanes_on'

custom tcp.custom.track_io_text_on  '' 0 '' 'custom_track_io_text_on'
custom tcp.custom.track_io_text_off '' 0 '' 'custom_track_io_text_off'
custom tcp.custom.track_io_darker   '' 0 '' 'custom_track_io_darker'

custom tcp.custom.folder_recarm   '' 0 '' 'custom_track_folder_recarm'


; ------ GLOBAL SETTINGS FOR TEXT COLORS ------

set luma                   + + * 0.299 [trackcolor_r] * 0.587 [trackcolor_g] * 0.114 [trackcolor_b]
set luma !trackcolor_valid 50

; macro for the color of text that is drawn over custom colors
macro text_over_custom element
    set element luma<=text_luma_thres + ?track_selected [220 220 220] [200 200 200] ?trackcolor_valid [9 9 9] [0] [25 25 25]                       
endmacro

; macro for the color of track labels, with tweaked custom color inheritance if a custom color is too dark
macro trackLabelColor element
    set element ?track_selected [235 235 235] [217 217 217]
    ;set element folderstate==1 * [trackcolor_r trackcolor_g trackcolor_b] ?track_selected 1.3 1.17 
endmacro

;----- END OF GLOBAL SETTINGS


macro paramPair scalar Idx n1 n2 n3 n4 n5 n6 n7 n8 n9 n10
	set scalar param##Idx<=1 n1 param##Idx==2 n2 param##Idx==3 n3 param##Idx==4 n4 param##Idx==5 n5 param##Idx==6 n6 param##Idx==7 n7 param##Idx==8 n8 param##Idx==9 n9 param##Idx>=10 n10
endmacro

; define all the script parameters used in this theme

define_parameter 'defaultV6' 'defaultV6_version' 1


define_parameter tcp_bg_color                   'TCP: Tint track panel' 1 0 1
define_parameter tcp_bg_sel_color               'TCP: Tint track panel only when selected' 0 0 1
define_parameter tcp_bg_dim                     'TCP: Dim track panel' 0 0 5

define_parameter tcp_side_fx                    'TCP: Show FX list on the side' 1 0 1
define_parameter tcp_sep_parms                  'TCP: Separate FX parameter list' 0 0 1
define_parameter tcp_side_parms                 'TCP: Show FX parameter list on the side' 1 0 1
define_parameter tcp_sep_sends                  'TCP: Separate sends list' 0 0 1
define_parameter tcp_side_sends                 'TCP: Show sends list on the side' 1 0 1
define_parameter tcp_show_side_buttons          'TCP: Move FX / IO buttons to the side' 0 0 1
define_parameter tcp_side_button_hide           'TCP: Hide side FX / IO buttons at minimum height' 1 0 1

define_parameter tcp_fx_margin                  'TCP: Margin between lists' 4 0 32
define_parameter tcp_fx_h                       'TCP: List row height' 17 0 200
define_parameter tcp_fx_section_width           'TCP: FX width' 84 20 200   
define_parameter tcp_fx_num_columns             'TCP: FX column limit' 5 1 8
define_parameter tcp_parm_section_width         'TCP: FX parameter width' 64 20 200
define_parameter tcp_parm_num_columns           'TCP: FX parameter column limit' 3 1 8
define_parameter tcp_send_section_width         'TCP: Send width' 64 20 200
define_parameter tcp_send_num_columns           'TCP: Send column limit' 3 1 8
define_parameter tcp_label_len_offs             'TCP: Name size offset' 0 0 400
define_parameter tcp_vol_len_offs               'TCP: Volume size offset' 0 0 400
define_parameter tcp_input_len_offs             'TCP: Input size offset' 0 0 400
define_parameter tcp_allow_shrink               'TCP: Allow shrinking long controls' 1 0 1
define_parameter tcp_expand_input               'TCP: Expand input to end of row' 1 0 1
define_parameter tcp_show_fixed_lanes           'TCP: Show fixed lanes button' 1 0 1
define_parameter tcp_show_folder_recarm         'TCP: Show folder label indicator' 0 0 1
define_parameter tcp_avoid_solo_flip          	'TCP: Avoid flipping mute/solo' 0 0 1

define_parameter mcp_fx_h                       'MCP: List row height' 17 0 200
define_parameter mcp_separator_w                'MCP: Separator layout width' 27 24 300
define_parameter mcp_extmix_offs                'MCP: Extmixer height offset' 0 -200 200
define_parameter master_mcp_extmix_offs         'Master MCP: Extmixer height offset' 0 -200 200
define_parameter master_mcp_hide_icon           'Master MCP: Hide track icon' 0 0 1

define_parameter text_luma_thres                'Text over color: Black text threshold' 130 0 255

define_parameter font100_main                   'Font override: 100% Main' 0 0 16
define_parameter font100_header                 'Font override: 100% Track name' 0 0 16
define_parameter font100_list                   'Font override: 100% FX/parameters/sends' 0 0 16

define_parameter font150_main                   'Font override: 150% Main' 0 0 16
define_parameter font150_header                 'Font override: 150% Track name' 0 0 16
define_parameter font150_list                   'Font override: 150% FX/parameters/sends' 0 0 16

define_parameter font200_main                   'Font override: 200% Main' 0 0 16
define_parameter font200_header                 'Font override: 200% Track name' 0 0 16
define_parameter font200_list                   'Font override: 200% FX/parameters/sends' 0 0 16

define_parameter user_notice                    '---------- For the options below use v6 theme adjuster ----------------------------------------------------' 0
define_parameter user_notice_line               '---------------------------------------------------------------------------------------------------------' 0

macro indexParams desc val min max idx
	define_parameter param##idx desc val min max
endmacro

;			description					val	min	max	idx
indexParams 'A_mcp_indent' 				1 	1 	7 	1
indexParams 'A_tcp_indent' 				4 	1 	7 	2
indexParams 'A_tcp_vol_size' 			1 	1 	7 	3
indexParams 'A_tcp_MeterSize' 			2 	1 	7 	4
indexParams 'A_tcp_Record_Arm' 			0 	0 	15 	5
indexParams 'A_tcp_Monitor' 			4 	0 	15 	6
indexParams 'A_tcp_Track_Name' 			0 	0 	15 	7
indexParams 'A_tcp_Volume' 				0 	0 	15 	8
indexParams 'A_tcp_Routing' 			0 	0 	15 	9
indexParams 'A_tcp_Effects' 			0 	0 	15 	10
indexParams 'A_tcp_Envelope' 			0 	0 	15 	11
indexParams 'A_tcp_Pan_&_Width' 		0 	0 	15 	12
indexParams 'A_tcp_Record_Mode' 		8 	0 	15 	13
indexParams 'A_tcp_Input' 				0 	0 	15 	14
indexParams 'A_tcp_Values'		 		2 	0 	15 	15
indexParams 'A_tcp_Meter_Values'		0 	0 	15 	16
indexParams 'A_tcp_MeterLoc'			2 	1 	3 	17
indexParams 'A_tcp_LabelSize'	 		5 	1 	7 	18
indexParams 'A_tcp_LabelMeasure' 		100	1 	200	19
indexParams 'A_tcp_InputSize'	 		5 	1 	7	20
indexParams 'A_envcp_labelSize'			6 	1 	7 	21
indexParams 'A_envcp_LabelMeasure' 		100	1 	200	22
indexParams 'A_envcp_fader_size' 		1 	1 	7 	23
indexParams 'A_envcp_folder_indent'		1 	0 	1 	24
indexParams 'A_mcp_Sidebar'				0 	1 	5 	25
indexParams 'A_mcp_Narrow'				0 	1 	5 	26
indexParams 'A_mcp_Meter_Expansion'		0 	1 	5 	27
indexParams 'A_mcp_Labels'				3 	1 	5 	28
indexParams 'A_mcp_meterExpSize' 		1 	1 	4 	29
indexParams 'A_mcp_border' 				1 	1 	5 	30

indexParams 'B_tcp_vol_size' 			4 	1 	7 	31
indexParams 'B_tcp_MeterSize' 			2 	1 	7 	32
indexParams 'B_tcp_Record_Arm' 			0 	0 	15 	33
indexParams 'B_tcp_Monitor' 			4 	0 	15 	34
indexParams 'B_tcp_Track_Name' 			0 	0 	15 	35
indexParams 'B_tcp_Volume' 				0 	0 	15 	36
indexParams 'B_tcp_Routing' 			0 	0 	15 	37
indexParams 'B_tcp_Effects' 			0 	0 	15 	38
indexParams 'B_tcp_Envelope' 			0 	0 	15 	39
indexParams 'B_tcp_Pan_&_Width' 		0 	0 	15 	40
indexParams 'B_tcp_Record_Mode' 		8 	0 	15 	41
indexParams 'B_tcp_Input' 				0 	0 	15 	42
indexParams 'B_tcp_Values'	 			2 	0 	15 	43
indexParams 'B_tcp_Meter_Values'	 	0 	0 	15 	44
indexParams 'B_tcp_MeterLoc' 			2 	1 	3 	45
indexParams 'B_tcp_InputSize'	 		5 	1 	7	46
indexParams 'B_tcp_LabelSize'	 		5 	1 	7 	47
indexParams 'B_mcp_Sidebar'				0 	1 	5 	48
indexParams 'B_mcp_Narrow'				0 	1 	5 	49
indexParams 'B_mcp_Meter_Expansion'		0 	1 	5 	50
indexParams 'B_mcp_Labels'				0 	1 	5 	51
indexParams 'B_mcp_meterExpSize' 		1 	1 	4 	52
indexParams 'B_mcp_border' 				1 	1 	5 	53

indexParams 'C_tcp_vol_size' 			1 	1 	7 	54
indexParams 'C_tcp_MeterSize' 			2 	1 	7 	55
indexParams 'C_tcp_Record_Arm' 			0 	0 	15 	56
indexParams 'C_tcp_Monitor' 			4 	0 	15 	57
indexParams 'C_tcp_Track_Name' 			0 	0 	15 	58
indexParams 'C_tcp_Volume' 				0 	0 	15 	59
indexParams 'C_tcp_Routing' 			0 	0 	15 	60
indexParams 'C_tcp_Effects' 			0 	0 	15 	61
indexParams 'C_tcp_Envelope' 			0 	0 	15 	62
indexParams 'C_tcp_Pan_&_Width' 		0 	0 	15 	63
indexParams 'C_tcp_Record_Mode' 		8 	0 	15 	64
indexParams 'C_tcp_Input' 				0 	0 	15 	65
indexParams 'C_tcp_Values'	 			2 	0 	15 	66
indexParams 'C_tcp_Meter_Values'		0 	0 	15 	67
indexParams 'C_tcp_MeterLoc'			2 	1 	3 	68
indexParams 'C_tcp_InputSize'	 		5 	1 	7	69
indexParams 'C_tcp_LabelSize'	 		5 	1 	7 	70
indexParams 'C_mcp_Sidebar'				1 	1 	5 	71
indexParams 'C_mcp_Narrow'				2 	1 	5 	72
indexParams 'C_mcp_Meter_Expansion'		0 	1 	5 	73
indexParams 'C_mcp_Labels'				1 	1 	5 	74
indexParams 'C_mcp_meterExpSize' 		1 	1 	4 	75
indexParams 'C_mcp_border' 				1 	1 	5 	76

indexParams 'A_trans_rate_size' 		1 	1 	7 	77
indexParams 'A_mcp_control_align' 		2 	1 	2 	78
indexParams 'A_tcp_control_align' 		3 	1 	3 	79
indexParams 'A_glb_track_label_color'	1 	0 	1 	80
indexParams 'A_tcp_sepSends'			1	1	2	81
indexParams 'B_tcp_sepSends'			1	1	2 	82
indexParams 'C_tcp_sepSends'			2	1	2 	83

; ---------------------------

; Global stuff : here are some often-used lines, set as definitions

set width_visible trackpanmode<4 [0] trackpanmode==6 [2] [1]  ; can be ==3 instead of <4 if width desired for 3.x
set gl_pan_color               width_visible{0}!=2 [0 0 0 120] [84 63 63]
set gl_width_color             width_visible{0}==1 [255 255 255 210] [63 84 63]
set gl_fader_swap              width_visible{0}==2 [0] [1]
text_over_custom val_color



#>---------------------------- TRACK CONTROL PANELS -----------------------------------

clear tcp.*

macro resetHides
	set hideValues 			0
	set hide_tcp.recarm  	0
	set hide_tcp.recmon  	0
	set hide_tcp.label  	0
	set hide_tcp.volume  	0
	set hide_pan_group  	0
	set hide_tcp.io  		0
	set hide_fx_group  		0
	set hide_tcp.env  		0
	set hide_tcp.recmode  	0
	set hide_input_group  	0
	set hide_values  		0
	set hide_meter_values	0
endMacro

macro shrinkLabelAndVolume min_label_len min_vol_len max_shrink_total

	set max_shrink_label - tcp_LabelSize + indent_offs - min_label_len recmon_w
	set max_shrink_label max_shrink_label<0 0

	set max_shrink_vol - tcp_Volsize min_vol_len
	set max_shrink_vol max_shrink_vol<0 0

    ; Calculate the ratio of max shrink values
    set shrink_ratio / max_shrink_label max_shrink_vol
    
    ; Calculate initial proportional shrink values
    set shrink_vol / max_shrink_total + shrink_ratio 1
    set shrink_label * shrink_vol shrink_ratio
    
    ; Check if either volume or label exceeds its max shrink
	set shrink_label shrink_label>max_shrink_label max_shrink_label
	set shrink_vol shrink_label>max_shrink_label - max_shrink_total max_shrink_label

	set shrink_label shrink_vol>max_shrink_vol - max_shrink_total max_shrink_vol
	set shrink_vol shrink_vol>max_shrink_vol max_shrink_vol

    set did_shrink 0
	set did_shrink max_shrink_total>0 shrink_label<=max_shrink_label shrink_vol<=max_shrink_vol 1
	set did_shrink !tcp_allow_shrink 0

	set tcp_LabelSize 	?did_shrink - . shrink_label
	set tcp_VolSize  	?did_shrink - . shrink_vol
endmacro

; Font-size         Windows     MacOS/Linux
; WALTER font 1     8           11
; WALTER font 2     9           12
; WALTER font 3     10          13
; WALTER font 4     11          15
; WALTER font 5     12          16
; WALTER font 6     13          17
; WALTER font 7     14          19
; WALTER font 8     15          20
; WALTER font 9     16          21
; WALTER font 10    17          23
; WALTER font 11    18          24
; WALTER font 12    19          25
; WALTER font 13    20          27
; WALTER font 14    21          28
; WALTER font 15    22          29
; WALTER font 16    23          31

macro calcFontSizes scale
    ; Windows font
    set main_font    os_type==0  scale==1 [1] scale==1.5 [4] [7]
    set list_font    os_type==0  scale==1 [1] scale==1.5 [4] [8]
    set header_font  os_type==0  scale==1 [2] scale==1.5 [7] [10]

    ; MacOS font
    set main_font   os_type==1  scale==1 [3] scale==1.5 [6]  [11]
    set list_font   os_type==1  scale==1 [2] scale==1.5 [8]  [12]
    set header_font os_type==1  scale==1 [5] scale==1.5 [10] [16]

    ; Linux font
    set main_font   os_type==2  scale==1 [2] scale==1.5 [7]  [11]
    set list_font   os_type==2  scale==1 [2] scale==1.5 [8]  [12]
    set header_font os_type==2  scale==1 [4] scale==1.5 [10] [15]

    ; User font overrides (only used when value > 0)
    set main_font   scale==1    font100_main==0 . font100_main \
                    scale==1.5  font150_main==0 . font150_main \
                    scale==2    font200_main==0 . font200_main

    set list_font   scale==1    font100_list==0 . font100_list \
                    scale==1.5  font150_list==0 . font150_list \
                    scale==2    font200_list==0 . font200_list

    set header_font scale==1    font100_header==0 . font100_header \
                    scale==1.5  font150_header==0 . font150_header \
                    scale==2    font200_header==0 . font200_header

    ; Font vertical offset
    set main_font_voffs     scale==1 0.5 scale==1.5 -0.5 -0.5
    set tcp_label_voffs     os_type==0  scale==1 1   scale==1.5  0   0 \
                            os_type==1  scale==1 1   scale==1.5  2   0 \
                            os_type==2  scale==1 1   scale==1.5  1   1

endMacro

macro floor value ; Fml...
    set floor_result 	value<01 00 value<02 01 value<03 02 value<04 03 value<05 04 value<06 05 value<07 06 value<08 07 \
				        value<09 08 value<10 09 value<11 10 value<12 11 value<13 12 value<14 13 value<15 14 value<16 15 \
				        value<17 16 value<18 17 value<19 18 value<20 19 value<21 20 value<22 21 value<23 22 value<24 23 \
				        value<25 24 value<26 25 value<27 26 value<28 27 value<29 28 value<30 29 value<31 30 value<32 31 \
				        value<33 32 value<34 33 value<35 34 value<36 35 value<37 36 value<38 37 value<39 38 value<40 39 \
				        value<41 40 value<42 41 value<43 42 value<44 43 value<45 44 value<46 45 value<47 46 value<48 47 \
				        value<49 48 value<50 49 value<51 50 value<52 51 value<53 52 value<54 53 value<55 54 value<56 55 \
				        value<57 56 value<58 57 value<59 58 value<60 59 value<61 60 value<62 61 value<63 62 value<64 63 \
				        value
endMacro

macro calcTcpSecs scale
	set element_h 								* scale 15  ; supercollapsed threshold is a bit smaller than the actual element size, in case our non-supercollapsed size is small due to UI scaling
	set supercollapsed							h<element_h{0} 1 0
	set element_h 								* scale 20
	set tcp_padding								* scale [7 7]
	set tcp_LabelSize_min						scale==2 100 scale==1.5 90 80 ; MOD longer default label for AUTO setting
	set tcp_LabelSize 							tcp_LabelPair{0}==0 tcpLabelAutoMeasured{0}<tcp_LabelSize_min{0} tcp_LabelSize_min{0} + tcpLabelAutoMeasured{0} 10 tcp_LabelPair

	set tcp_PanSize								width_visible{0}==0 20 43
	set this_tcp_indent							* * scale tcp_indent{0} folderdepth{0}
	set soloFlip_h								* scale 52 ; height to switch solo&mute from stacked to side-by-side
	set is_solo_flipped							?tcp_avoid_solo_flip 0 h>=soloFlip_h{0} 1 0
	set folder_sec								* scale tcp_control_align{0}==1 \
													+ [0 0 20] * [0 0 1] * tcp_indent{0} maxfolderdepth{0} \
													+ [0 0 20] * [0 0 1] * tcp_indent{0} folderdepth{0}
	set tcp_MeterSize_min						* scale 18
	set meter_sec								+ + + * scale + [0 0 tcp_MeterSize{0}] [0 0 33] [folder_sec{2} 0 0 h] !is_solo_flipped tcp_MeterSize{0}<tcp_MeterSize_min{0} * scale [0 0 17] [0] [0] [0]
	set meterRight								tcp_MeterLoc{0}<2 tcp_MeterLoc{0} ?recarm 0 1
	set meter_sec								?meterRight{0} - [w  0 meter_sec meter_sec] [meter_sec{2}]
	set main_sec 								+ - - [folder_sec{2} 0 w h] [0 0 folder_sec{2}] [0 0 meter_sec{2}] !meterRight{0} [meter_sec{2}] [0]

    ;*** FTC MOD (part 1): FX-list on the right side / shrink behavior ***

	set indent_offs tcp_control_align==2 * - maxfolderdepth folderdepth tcp_indent 0

	set side_margin_r   * scale 4
	set list_margin     * scale tcp_fx_margin

	; Calculate side section width based on settings
    set has_tcp_fxlist reaper_version<=683 tcp_fxparms>0 1 0 ?tcp_fxlist_enabled 1 0
	set default_behavior !has_tcp_fxlist 1 0

	set show_side_fx !has_tcp_fxlist 0 !tcp_side_fx 0 1
	set fx_sec_w * scale tcp_fx_section_width
	set side_sec_w + side_margin_r ?show_side_fx fx_sec_w 0
	set sec_cnt ?show_side_fx 1 0

    set tcp_sepSends ?tcp_sep_sends 1
	set show_side_sends !tcp_sends_enabled 0 !tcp_sepSends 0 !tcp_side_sends 0 1
	set send_sec_w * scale tcp_send_section_width
	set side_sec_w ?show_side_sends + send_sec_w
	set sec_cnt ?show_side_sends + 1

	set show_side_parms ?default_behavior 0 !tcp_side_parms 0 !tcp_sep_parms 0 1
	set parm_sec_w * scale tcp_parm_section_width
	set fx_sec_w !has_tcp_fxlist parm_sec_w
	set side_sec_w ?show_side_parms + parm_sec_w
	set sec_cnt ?show_side_parms + 1

	set show_side_sec ?show_side_fx 1 ?show_side_parms 1 ?show_side_sends 1 0
	set side_sec_w + . * list_margin - sec_cnt 1
	set side_sec_w !show_side_sec 0

    ; Check placement of side buttons
    set show_side_fx_button !tcp_show_side_buttons 0 ?show_side_fx    1 ?show_side_parms 1 0
    set show_side_io_button !tcp_show_side_buttons 0 ?show_side_sends 1 0
    set show_side_buttons   ?show_side_fx_button 1  ?show_side_io_button 1 0

    ; Custom width offset
	set min_fader_w 	20
    set tcp_VolSize     tcp_VolPair{0}
    set tcp_InSize      tcp_InPair{0}
    
	set tcp_VolSize 	tcp_VolSize>20  + tcp_vol_len_offs 
	set tcp_InSize 		tcp_InSize>49   + tcp_input_len_offs 
	set tcp_LabelSize 	+ tcp_label_len_offs 

    set recmon_w 15
    set recarm_w 20
    set io_w     28

    ; Add 15 width for record monitor button, substract later if not visible
    set tcp_LabelSize - . recmon_w 
    set tcp_LabelSize	+ indent_offs

    ; Calculate position where pan knob ends
	set tcp_LabelSize 	?hide_tcp.label 	0
	set tcp_VolSize 	?hide_tcp.volume 	0
	set tcp_InSize 	    ?hide_input_group 	0

    set pan_end     + * scale + + + tcp_LabelSize tcp_VolSize recmon_w 1 * 2 tcp_padding{0}
	set pan_end 	!hide_tcp.recarm    + . * scale recarm_w
	set pan_end 	!hide_pan_group     + . + * scale tcp_PanSize tcp_padding{0}

    set include_io ?hide_tcp.io 0 tcp_VolSize>20 0 ?show_side_sends 0 1
    set added_io_w + * scale io_w tcp_padding{0}

    ; Shrink label and volume until route button has to be hidden (if not already hidden)
	set shrink_w / - + + pan_end side_sec_w ?include_io added_io_w 0 main_sec{w} scale
	set shrink_w ?tcp_allow_shrink !show_side_sec !include_io 0

	shrinkLabelAndVolume 66 40 shrink_w
    set include_io  !did_shrink 0

    ; Shrink label and volume until side section has to be hidden (if not already hidden)
	set shrink_w / - + + pan_end side_sec_w ?include_io added_io_w 0 main_sec{w} scale
	set shrink_w ?did_shrink 0 ?tcp_allow_shrink !show_side_sec  0

	shrinkLabelAndVolume 42 40 shrink_w

	set show_side_sec   !tcp_allow_shrink shrink_w>0 0
	set show_side_sec   ?tcp_allow_shrink shrink_w>0 !did_shrink 0
	set side_sec_w          !show_side_sec 0
    set show_side_fx_button !show_side_sec 0
    set show_side_io_button !show_side_sec 0
    set show_side_buttons   !show_side_sec 0

	set main_sec - . side_sec_w@w

    ; Shrink label and volume until label has to be flipped
    set include_io ?hide_tcp.io 0 tcp_VolSize>20 0 1
	set shrink_w / - + pan_end ?include_io added_io_w 0 main_sec{w} scale
	set shrink_w ?show_side_sec 0

	shrinkLabelAndVolume 35 40 shrink_w

	set labelFlip 		?show_side_sec 0 shrink_w<=0 0 !did_shrink 1 0
    set labelEnd		+ tcp_LabelSize{0} 54 ; tune 54 by eye. Sorry.
	set labelFlip 		!tcp_allow_shrink main_sec{2}<labelEnd{0} 1 0

	set hide_folder_recmon !tcp_show_folder_recarm 0 !folderstate==1 0 ?recarm 0 1
	set tcp_LabelSize   ?hide_tcp.recmon  + . recmon_w ?hide_folder_recmon + . recmon_w  
	set tcp_VolSize  	?labelFlip                  20

	set mainCollapse_w							* scale 90
	set mainCollapse							!labelFlip 0 main_sec{2}<mainCollapse_w{0} 1 0 ; collapse main_sec to enough for a flipped label if its too small to be useful, then scale meter to fill the gap
	
    ;*** FTC MOD end ***

	set main_sec 								?supercollapsed{0} [0] ?mainCollapse{0} ?meterRight{0} \
																							[folder_sec{2} 0 element_h{0} h] \
																							- [w 0 element_h{0} h] [element_h{0}] \
																						?labelFlip{0} - main_sec [0 0 element_h{0}] main_sec ; reduce main_sec if label is flipped
	set main_eff								?mainCollapse{0} 0 main_sec{2} ; effective width of main_sec, used to determine row end
	set meter_sec 								?supercollapsed{0} [0] ?mainCollapse{0} ?meterRight{0} \
													- + [main_sec 0 w h] [main_sec{2} 0] + [0 0 main_sec{0}] [0 0 main_sec] \
													- - [folder_sec{2} 0 w h] [0 0 folder_sec] * scale [0 0 20]
	set tcpMinSize								+ + * scale !is_solo_flipped 60 44 folder_sec{2} element_h{0}
	set tcp.size 								+ * scale [300 100] [0 0 tcpMinSize{0}]
	set tcp_LabelSize							?labelFlip{0} [0] 	; not included in mainsec calculations if flipped
	set tcp_squash_height 						+ + * scale 20 tcp_padding{1} tcp_padding{1}
	set h_less_border							- h 1
	set tcp_padding								h_less_border{0}<tcp_squash_height{0} + [tcp_padding] * [0 1] / - h_less_border{0} element_h 2
	set tcpNullElement							+ [main_sec] + [tcp_padding tcp_padding] * scale [0 0 0 20]
	set previous_element 						tcpNullElement		; Let's start at the very beginning. A very good place to start.
	set main_sec_last tcp_padding{y}
endMacro

macro doHideLogic element paramIdx
	set hide_##element param##paramIdx##&1 ?mixer_visible 1 0
	set hide_##element param##paramIdx##&2 !track_selected 1 hide_##element
	set hide_##element param##paramIdx##&4 !recarm 1 hide_##element
	set hide_##element param##paramIdx##&8 1 hide_##element
endmacro

macro resizeInput is_next_row
    set min_input_w         * scale element{y}<element_h 104 49
    set fill_end            element{y}<element_h main_eff{0} ?labelFlip main_eff{0} first_row_r
    set remainder           tcp_InSize<=49 0 - this_end{0} fill_end
    set remainder           remainder<0 !tcp_expand_input 0
    set remainder           remainder>0 !tcp_allow_shrink 0
    set adj_width           - s_width remainder
    set min_input_w         element{y}>=element_h !labelFlip !tcp_control_align==1 + . * * scale tcp_indent - maxfolderdepth folderdepth
    set element		        element{w}>0 ?fill adj_width>min_input_w - . remainder@w
    set this_end			element{w}>0 ?fill adj_width>min_input_w - . remainder
    set this_end			element{w}>0 ?fill adj_width<=min_input_w ?tcp_allow_shrink ?tcp_expand_input !is_next_row  + main_eff{0} 1
endMacro

macro then element width use_padding fill exclude
	; apply scale
	set s_width				* width scale

	set prev_end 			+ [previous_element] [previous_element{2}]									; find the end of the previous element
	set element 			[prev_end previous_element s_width element_h]								; ...and place the element there
	
    set is_hidden           ?hide_##element{0} 1 ?exclude 1 0   ; FTC MOD
    set element				?is_hidden [. . 0 element_h]										            ; apply hide params

	; x-axis
	set element 			?use_padding !is_hidden + [tcp_padding] 							        ; if x-padding is required, add it
	set this_end			- + + [element] [element{2}] [tcp_padding] [main_sec]						; okay, and what does that make the x-end of this element, realtive to main_sec?
    
    resizeInput 0 ;FTC MOD: Shrink/expand input ***
    
	set element				?mainCollapse{0} [. . 0 0]

	; y-axis
	set next_row			+ [0 element] [0 previous_element{3}] 										; find the y of the next row
	set element 			this_end{0}>main_eff{0} + [main_sec next_row element element] tcp_padding	; move it to the next row
	set this_end			- + + [element] [element{2}] [tcp_padding] [main_sec]						; recalculate this_end, now that the element is on a new row
	
    resizeInput 1 ;FTC MOD: Shrink/expand input ***
    
	set element				this_end{0}>main_eff{0} [. . 0 0]											; ... and cull if there's still not enough main_sec width to fit
	set this_y_end			+ + element{1} element{3} tcp_padding{1} 									; find its bottom
	set element				this_y_end{0}>main_sec{3} [0] !previous_element{2} !previous_element{3} [0] . .					; cull if there's not enough height to fit, or if previous_element was culled
    
    ;FTC MOD Gather info on where rows end
    set first_row_r 		element{y}<element_h this_end{0}
    set any_row_r   		element{w}>0 this_end{0}>any_row_r{0} this_end{0}<=main_eff{0} this_end{0}

	; values readout control
	set valuesRoom			- - main_sec{3} this_y_end{0} tcp_padding{1}								; is the remaining height after this element enough for the values to fit?
	set hideValues			valuesRoom{0}<8 element{3}!=0 [1] . .										; if not, and this element isn't already hidden, cull the values

	set previous_element element
	set main_sec_last 		element{w}>0 this_y_end{0} .
	
endmacro

; ---- MODDING WITH 'THEN' ----
Elements within the main section of the TCP flow one after another based on the previous element's position and size,
and will move to the next row if necessary, and hide if there's not enough room. The order they appear is determined in
the 'then' sections. Because of the way WALTER's syntax works, it is not possible to do this with a script. However,
its very easy for you to mod ...just change the line order in the 'then'' section.
; -----------------------------

macro drawTcp scale
	;*** FTC MOD
	; Add v7 custom element background color to meter section
	custom tcp.custom.meter_sec_bg
	set tcp.custom.meter_sec_bg + meter_sec * scale [1 0 -1]
	set tcp.custom.meter_sec_bg.color [0 0 0 0 38 38 38 0]

	; Set any_row_r to main_sec width (only happens when all elements are hidden in adjuster)
	set any_row_r any_row_r==0 main_sec{w}

    ; Add track background overlays
    set bg_sec + + main_sec side_sec_w@w * scale [-2 0 4]
	set bg_sec tcp_control_align==1 + * [-1 0 2] * - maxfolderdepth folderdepth tcp_indent
	set bg_sec ?supercollapsed [0 0 w h]
    
    set show_solid_bg       ?track_selected 0 ?tcp_bg_sel_color 1 !tcp_bg_color 1 0
    set show_solid_bgsel    !track_selected 0 ?tcp_bg_sel_color 0 !tcp_bg_color 1 0

    set tcp.custom.bg_solid     !show_solid_bg     [0] bg_sec
    set tcp.custom.bgsel_solid  !show_solid_bgsel  [0] bg_sec

    set tcp.custom.bg_dimmed_1      !tcp_bg_dim==1 [0] ?track_selected [0]  bg_sec
    set tcp.custom.bg_dimmed_2      !tcp_bg_dim==2 [0] ?track_selected [0]  bg_sec
    set tcp.custom.bg_dimmed_3      !tcp_bg_dim==3 [0] ?track_selected [0]  bg_sec
    set tcp.custom.bg_dimmed_4      !tcp_bg_dim==4 [0] ?track_selected [0]  bg_sec
    set tcp.custom.bg_dimmed_5      !tcp_bg_dim==5 [0] ?track_selected [0]  bg_sec

    set tcp.custom.bgsel_dimmed_1   !tcp_bg_dim==1 [0] !track_selected [0]  bg_sec
    set tcp.custom.bgsel_dimmed_2   !tcp_bg_dim==2 [0] !track_selected [0]  bg_sec
    set tcp.custom.bgsel_dimmed_3   !tcp_bg_dim==3 [0] !track_selected [0]  bg_sec
    set tcp.custom.bgsel_dimmed_4   !tcp_bg_dim==4 [0] !track_selected [0]  bg_sec
    set tcp.custom.bgsel_dimmed_5   !tcp_bg_dim==5 [0] !track_selected [0]  bg_sec

    ; ***********

	set tcp.foldercomp              			?supercollapsed{0} [0] + + [this_tcp_indent] [folder_sec] * scale [0 0 18 19]
	set tcp.folder                  			?supercollapsed{0} [0] + [this_tcp_indent] * [1 scale{0} scale{0} scale{0} 1 1 1 1] [folder_sec 86 18 14 0 1 0 1]
	  set tcp_folder_min						* scale 32
	  set tcp.folder                			h<tcp_folder_min{0} folderstate<0 . [0] .

    ;*** FTC MOD add custom folder indent images for TCP ***
	front tcp.custom.folder_1-1 tcp.custom.folder_1-2 tcp.custom.folder_1-4 tcp.custom.folder_1-8 tcp.folder 

	set tcp.custom.folder_1-1 			folderstate==1 param2{0}>=5 tcp.folder [0]
	set tcp.custom.folder_1-2 			folderstate==1 param2{0}==4 tcp.folder [0]
	set tcp.custom.folder_1-4 			folderstate==1 param2{0}==3 tcp.folder [0]
	set tcp.custom.folder_1-8 			folderstate==1 param2{0}==2 tcp.folder [0]
    ;**************

	set tcp.trackidx                 			+ + [0 0 this_tcp_indent] [folder_sec 0 0 0 0 0 0 1] * scale [0 0 18 100]
	  set tcp.trackidx 							?supercollapsed{0} + tcp.trackidx * scale [0 0 0 40]
	  set tcp_idx_margin						trackidx>999 0 - / h 2 * scale 18
	  set tcp.trackidx.margin        			+ [this_tcp_indent] + [0 tcp_idx_margin 0 tcp_idx_margin 0.5] folderstate<0 * scale [0 -6 0 6] folderstate==1 * scale [0 16] 0
	  set tcp.trackidx.margin					trackidx>999 h<tcp_squash_height{0} [. . . . 1] 
	  set tcp.trackidx.font						main_font
	  text_over_custom 							tcp.trackidx.color
	  set tcp.trackidx.color					?track_selected [24 24 24] .
	set tcp.mute               					+ + [meter_sec] [meter_sec{2}] ?is_solo_flipped * scale [-25 3 21 20] + [0 tcp_padding] * scale [-43 0 21 20]
	set tcp.solo 								?is_solo_flipped + + [0 2] tcp.mute [0 tcp.mute{3}] \
																- + tcp.mute [tcp.mute{2}] [2]
	set phaseHide_h								+ + + * scale 12 element_h tcp.solo{1} tcp.solo{3}
    set tcp.phase 								h<phaseHide_h{0} [0] + * scale [3 -24 16 20] [tcp.solo meter_sec{3}]
    
    ;*** FTC MOD add custom fixed lanes button
    front tcp.custom.fixed_lanes_on tcp.custom.fixed_lanes_off

	set fixed_lanes_hide_h					    + phaseHide_h{0} * scale 14
    set tcp.custom.fixed_lanes_on 				h<fixed_lanes_hide_h{0} [0] + * scale [1 -47 20 24] [tcp.solo meter_sec{3}]
    set tcp.custom.fixed_lanes_off				tcp.custom.fixed_lanes_on

    set tcp.custom.fixed_lanes_on                trackfixedlanes==0 [0] !tcp_show_fixed_lanes [0]
    set tcp.custom.fixed_lanes_off               trackfixedlanes>0  [0] !tcp_show_fixed_lanes [0]
    ;**************

set tcp.meter                   			- - - + meter_sec * scale [5 2 -5 -5] [0 0 tcp.mute{2}] [0 0 tcp_padding{0}] !is_solo_flipped * scale [0 0 18] [0]
set tcp.meter + 1@h ; FTC fix margin error

	  set tcp.meter.vu.div                    	[1]
	  set tcp.meter.readout.color             	?hide_meter_values{0} [0 0 0 0 0 0 0 0] [100 100 100 255 255 183 171 255]
	  set tcp.meter.scale.color.lit.bottom    	?hide_meter_values{0} [0 0 0 0 0 0 0 0] [0 0 0 170 0 0 0 0]
	  set tcp.meter.scale.color.lit.top       	?hide_meter_values{0} [0 0 0 0 0 0 0 0] [0 0 0 170 0 0 0 0]
	  set tcp.meter.scale.color.unlit.bottom  	?hide_meter_values{0} [0 0 0 0 0 0 0 0] ?recarm [255 64 0 255 0 0 0 0] [255 255 255 60 0 0 0 0]
	  set tcp.meter.scale.color.unlit.top     	?hide_meter_values{0} [0 0 0 0 0 0 0 0] ?recarm [255 64 0 255 0 0 0 0] [255 255 255 60 0 0 0 0]
	set tcp.label 								?labelFlip{0} + [main_sec 0 1 -2] [main_eff{0} 0 element_h{0} h]
    set tcp.label 								?labelFlip{0} + . * scale [-2 0 4] ; FTC MOD
		set tcp.label.font					    header_font
        set tcp.label.margin                    * scale [4 0 2 0] ; FTC MOD
        set tcp.label.margin                    ?labelFlip + 0.5@ls
        set tcp.label.margin                    + tcp_label_voffs@y
	trackLabelColor tcp.label.color   
	set tcp.fx 									!fx_group{2} [0] + [fx_group fx_group] + * scale [0 0 20] [0 0 0 element_h{0}]
	set tcp.fxbyp 								!fx_group{2} [0] + + [tcp.fx tcp.fx] [tcp.fx{2}] + * scale [0 0 16] [0 0 0 element_h{0}]
	set tcp.fxin 								!input_group{2} [0] + [input_group input_group] + * scale [0 0 29] [0 0 0 element_h{0}]
	set tcp.recinput							!input_group{2} [0] - + + [tcp.fxin tcp.fxin] [tcp.fxin{2} 0 input_group] [0 0 0 element_h{0}] [0 0 tcp.fxin{2}]
	set tcp.recinput							scale==1.5 + 0.5@x
		set tcp.recinput.color           		?track_selected [255 255 255] [225 225 225]
		set tcp.recinput.font					main_font
		set tcp.recinput.margin        			* [scale scale scale scale 1] [0 0 20 0 0.5]
		set tcp.recinput.margin        			+ main_font_voffs@y
        ;*** FTC MOD *** Hide recinput text at narrow widths; Note: Setting opacity seems unpredictable so also settting margin
        set min_input_text_w * scale 30
        set tcp.recinput.color           		?tcp.recinput{w}<min_input_text_w [. . . 1] [. . . 255]
        set tcp.recinput.margin           		?tcp.recinput{w}<min_input_text_w + min_input_text_w@x
        ;*** MOD end ***;

    ; FTC MOD Add label folder indicator      
    set tcp.custom.folder_recarm !tcp_show_folder_recarm [0] !folderstate==1 [0] ?recarm [0] tcp.recarm
    front tcp.custom.folder_recarm

	set tcp.pan 								!pan_group{2} [0 0 0 0] width_visible{0}==2 \
													* [pan_group pan_group pan_group pan_group element_h{0}] [1 1 1 0.5] \
													[pan_group pan_group element_h{0} element_h{0}] ; knobs have the same h and w
		set tcp.pan.fadermode         			gl_fader_swap
		set tcp.pan.color              			gl_pan_color
	set tcp.width								width_visible{0}==0 [0] !pan_group{2} [0 0 0 0] width_visible{0}==2 \
													+ [tcp.pan tcp.pan tcp.pan tcp.pan] [0 tcp.pan{3}] \
													- + [pan_group pan_group element_h{0} element_h{0}] [pan_group{2}] [element_h{0}]
		set tcp.width.fadermode        			gl_fader_swap
		set tcp.width.color            			gl_width_color
	set volume_fader_w							* 21 scale
	set tcp.volume.fadermode         			tcp.volume{w}>volume_fader_w{0} [0] [1]
		set tcp.volume.color           			[0 0 0]
	set hideValues								?hide_values{0} 1 .
	set values_sec								hideValues{0}==1 [0] + [main_sec main_sec_last{0} any_row_r{0}] * scale [0 0 0 9] ; FTC MOD
	set tcp.volume.label						!values_sec{0} [0] + + * scale [0 -2 42 2] [values_sec values_sec 0 values_sec] [tcp_padding]
		set tcp.volume.label.font				main_font
		set tcp.volume.label.margin    			* scale [0 0 0 0 0]
        set tcp.volume.label.margin             + main_font_voffs@y
		set tcp.volume.label.color     			val_color
	set tcp.width.label							!values_sec{0} [0] main_eff{0}<122 [0] width_visible{0}==0 [0]	+ + * scale [0 -2 36 2] [values_sec values_sec 0 values_sec] - [values_sec{2}] + * scale [36] [tcp_padding]
		set tcp.width.label.margin     			+ tcp.volume.label.margin 1@ls ; FTC MOD  + * scale [0 1] + OSoffs [0 0 0 0 1]
		set tcp.width.label.font				main_font
		set tcp.width.label.color      			width_visible{0}!=2 val_color + [90 140 90] * val_color [0.2 0.5 0.2]
	set tcp.pan.label							!values_sec{0} [0] main_eff{0}<122 [0] + + * scale [0 -2 36 2] [values_sec values_sec 0 values_sec] - [values_sec{2}] + + * scale [36] [tcp_padding] [tcp.width.label{2}]
		set tcp.pan.label.margin       			tcp.width.label.margin
		set tcp.pan.label.font					tcp.width.label.font
		set tcp.pan.label.color        			width_visible{0}!=2 val_color + [120 70 70] * val_color [0.5 0.2 0.2]

	; x-axis label hiding
	set tcpVolLabelSpace						+ tcp.volume.label{2} tcp_padding{0}
	set tcp.volume.label						main_sec{2}<tcpVolLabelSpace{0} [0] .
	set tcpPanLabelSpace						+ + + tcp.volume.label{2} tcp_padding{0} tcp.pan.label{2} tcp.width.label{2}
	set tcp.pan.label							main_sec{2}<tcpPanLabelSpace{0} [0] .
	set tcpWidthLabelSpace						+ + tcp.volume.label{2} tcp_padding{0} tcp.width.label{2}
	set tcp.width.label							main_sec{2}<tcpWidthLabelSpace{0} [0] .

	set tcp.dragdropinfo 						+ [this_tcp_indent this_tcp_indent] * scale [0 tcp_indent{0}]

	;*** FTC MOD (part 2): FX-list on the right side ***

	set tcp.fxparmArea         						- + + + [main_sec main_sec_last{0}] [tcp_padding] hideValues{0}==1 [0] + [0 values_sec{3}] [0 tcp_padding] \
													+ - - [0 0 main_sec] [0 0 tcp_padding{0}] [0 0 tcp_padding{0}] \
													- - - [0 0 0 main_sec] [0 0 0 main_sec_last{0}] [0 0 0 tcp_padding{1}] hideValues{0}==1 [0] + [0 0 0 values_sec] [0 0 0 tcp_padding{1}] \
													[0]
	
    ; Limit fpxarmArea width to last row element
    set fxparmArea_w - + - any_row_r tcp.fxparmArea{x} main_sec{x} tcp_padding{0}
    set tcp.fxparmArea [. . fxparmArea_w .]

	set double_row_h    * scale 43
    set stick_row_h     * scale 70
    set is_dense_row   h>=double_row_h h<stick_row_h 1 0

    ; Calculate placement for side section
    set show_side_buttons h<double_row_h ?tcp_side_button_hide 0

    set side_button_y  h>stick_row_h tcp_padding{y} h<double_row_h tcp_padding{y} * scale 3
    set side_button_h  * scale ?is_dense_row 19 20

	set side_x  - tcp.size{0} side_sec_w{0} 
	set side_x  ?meterRight{0} - . meter_sec{w} 
    set side_y  h>stick_row_h  tcp_padding{y} * scale 1
    set side_y  ?show_side_buttons + + side_button_y side_button_h * scale h>stick_row_h 1 -1
	set side_h  - h side_y{0}

	; Calculate fx height
	set fx_h * scale tcp_fx_h
    set fx_h ?show_side_buttons ?is_dense_row tcp_fx_h>17 tcp_fx_h<21 * scale 17

	set scrollbar_size  * scale 4
	set min_fx_h  - side_h + scrollbar_size * scale 1
    set fx_h fx_h>min_fx_h min_fx_h

    ; Calculate how many fx/params/sends are shown on the side to check if scrollbar will be visible

    ; Limit side section height to a multiple of fx height
    ; This avoids stretched fx slots on MacOS and helps align scrollbars
	set fx_rows  / - side_h * 1.5 scrollbar_size fx_h
    floor fx_rows
    set fx_rows floor_result<1 1 floor_result

    set show_side_fx    !show_side_sec 0
	set show_side_sends !show_side_sec 0
	set show_side_parms !show_side_sec 0

	set side_fx_cnt     ?show_side_fx       fx_cnt 0
	set side_fx_cnt     ?show_side_fx       !tcp_sep_parms 	+ fx_parm_cnt
	set side_fx_cnt     ?show_side_fx       !tcp_sepSends 	+ send_cnt
	set side_parm_cnt   ?show_side_parms    fx_parm_cnt 0
	set side_parm_cnt   !tcp_sepSends       + send_cnt
	set side_send_cnt   ?show_side_sends    send_cnt 0

    set scrollbar_side_fx_cnt     * fx_rows tcp_fx_num_columns 
    set scrollbar_side_parm_cnt   * fx_rows tcp_parm_num_columns 
    set scrollbar_side_send_cnt   * fx_rows tcp_send_num_columns 

    set show_scrollbar  0
	set show_scrollbar  ?show_side_fx 	 tcp_fx_num_columns>1   side_fx_cnt>=scrollbar_side_fx_cnt      1
	set show_scrollbar  ?show_side_parms tcp_parm_num_columns>1	side_parm_cnt>=scrollbar_side_parm_cnt  1
	set show_scrollbar  ?show_side_sends tcp_send_num_columns>1	side_send_cnt>=scrollbar_side_send_cnt  1

    ; Move row down a bit if scrollbar isn't visible
    set side_y  !show_side_buttons !show_scrollbar + . * scale 1

    set fx_rows_h * fx_rows fx_h
    set remaining_h - side_h + fx_rows_h scrollbar_size

    ; Reduce scrollbar gap height if there isn't much space
    set scrollbar_gap   scale==2 3 scale=1.5 2 1
    set scrollbar_gap   remaining_h<scrollbar_size - . scale==2 2 scale=1.5 2 1
    set scrollbar_size  remaining_h<scrollbar_size - . scale==2 2 scale=1.5 2 1

    ; Center rows, but when more than 3 rows stick to tcp_padding
    set stick_to_padding h>stick_row_h 1 fx_rows>=3 1 0
    set side_button_gap * scale 1
    set side_button_gap h>stick_row_h fx_rows>=3 * . 2 remaining_h>side_button_gap * . 2
    set remaining_h     ?show_side_buttons  - . + side_button_gap * scale ?show_scrollbar 3 1
    set remaining_h     remaining_h<0 0

    set stick_y - tcp_padding{y} * scale !show_side_buttons 1 0
    set side_y  !show_side_buttons h<=stick_row_h + / remaining_h 2
    set side_y  !show_side_buttons h>stick_row_h stick_y side_y>stick_y fx_rows>=3 stick_y
    
    set side_button_y   ?show_side_buttons  h>=double_row_h h<=stick_row_h + / remaining_h 2
    set side_button_y   ?show_side_buttons  h>stick_row_h stick_y side_button_y>=stick_y  stick_y
    set side_y          ?show_side_buttons  + + side_button_y side_button_h side_button_gap

    set side_h + fx_rows_h scrollbar_size

    ; Place side lists
	set tcp.fxparm [0]
	set tcp.fxparm ?show_side_fx [side_x{0} side_y{0} fx_sec_w{0} side_h{0} 1 0 1 0]
	set tcp.fxparm ?show_side_fx mainCollapse{0}==1 [0] supercollapsed{0}==1 [0] 
    
    set show_side_fx_button !show_side_buttons 0
    set tcp.fx      ?show_side_fx   ?show_side_fx_button    + [side_x{0} side_button_y{0} fx_sec_w{0} side_button_h{0} 1 0 1 0] * scale -16@w
	set tcp.fxbyp 	?show_side_fx   ?show_side_fx_button    + + [tcp.fx tcp.fx 0 side_button_h{0} 1 0 1 0] tcp.fx{w}@x * scale 16@w
    set tcp.fxparm  ?show_side_buttons  h<double_row_h [0]

	set side_x ?show_side_fx + + fx_sec_w list_margin

	set tcp.fxlist [0]
	set tcp.fxlist ?show_side_parms [side_x{0} side_y{0} parm_sec_w{0} side_h{0} 1 0 1 0] 

    set tcp.fx      ?show_side_parms    ?show_side_fx_button    !show_side_fx   + [side_x{0} side_button_y{0} parm_sec_w{0} side_button_h{0} 1 0 1 0] * scale -16@w
	set tcp.fx      ?show_side_parms    ?show_side_fx_button    ?show_side_fx   + + . list_margin@w parm_sec_w@w
    set tcp.fxbyp 	?show_side_parms    ?show_side_fx_button	+ + [tcp.fx tcp.fx 0 side_button_h{0} 1 0 1 0] tcp.fx{w}@x * scale 16@w
    set tcp.fxlist  ?show_side_buttons  h<double_row_h [0]

	set side_x ?show_side_parms + + parm_sec_w list_margin

	set tcp.sendlist [0]
	set tcp.sendlist ?show_side_sends [side_x{0} side_y{0} send_sec_w{0} side_h{0} 1 0 1 0]

    set show_side_io_button !show_side_buttons 0
    set tcp.io 	        ?show_side_io_button [side_x{0} side_button_y{0} send_sec_w side_button_h{0}  1 0 1 0]
    set tcp.sendlist    ?show_side_buttons h<double_row_h [0]

    set tcp.custom.track_io_darker      !show_side_io_button [0]  send_cnt==0 [0]   tcp.io
    set tcp.custom.track_io_text_on     !show_side_io_button [0]  send_cnt==0 [0] + tcp.io ?is_dense_row * scale 0@y 0
    set tcp.custom.track_io_text_off    !show_side_io_button [0]  send_cnt>0  [0] + tcp.io ?is_dense_row * scale 0@y 0
    front tcp.custom.track_io_text_on tcp.custom.track_io_text_off   tcp.custom.track_io_darker

    ; Calculate list element widths (fonts) based on user width and columns
    set col_gap * scale 2
	set min_w_limit * scale 15

	set fx_min_w - / - fx_sec_w{0} * col_gap - tcp_fx_num_columns 1 tcp_fx_num_columns * scale 2.5
	set fx_min_w tcp_fx_num_columns{0}==1 * fx_sec_w{0} 0.6
	set fx_min_w fx_min_w{0}<min_w_limit{0} min_w_limit

	set send_min_w - / - send_sec_w{0} * col_gap - tcp_send_num_columns 1 tcp_send_num_columns * scale 2.5
	set send_min_w tcp_send_num_columns{0}==1 * send_sec_w{0} 0.6
	set send_min_w send_min_w{0}<min_w_limit{0} min_w_limit

	set parm_min_w - / - parm_sec_w{0} * col_gap - tcp_parm_num_columns 1 tcp_parm_num_columns * scale 2.5
	set parm_min_w tcp_parm_num_columns{0}==1 * parm_sec_w{0} 0.6
	set parm_min_w parm_min_w{0}<min_w_limit{0} min_w_limit
	set fx_min_w !has_tcp_fxlist parm_min_w

    set lspacing tcp_fx_h>=34 0 1000
	set tcp.fxlist.font		[list_font{0} fx_h{0} fx_min_w{0}   fx_sec_w{0}   lspacing{0} 0 scrollbar_gap{0} scrollbar_size{0}]
	set tcp.sendlist.font	[list_font{0} fx_h{0} send_min_w{0} send_sec_w{0} lspacing{0} 0 scrollbar_gap{0} scrollbar_size{0}]
	set tcp.fxparm.font		[list_font{0} fx_h{0} parm_min_w{0} parm_sec_w{0} lspacing{0} 0 scrollbar_gap{0} scrollbar_size{0}]

	set tcp.fxparm.margin  	* [scale scale scale scale 1 scale 1 scale]  [1 0 2 0 0 14 0.5 col_gap{0}]
	set tcp.sendlist.margin	* [scale scale scale scale 1 scale 1 scale]  [2 0 1 0 0 14 0.5 col_gap{0}]
	set tcp.fxlist.margin	* [scale scale scale scale 1 scale 1 scale]  + [2 0 2 0 0 14 0.5 col_gap{0}] ?tcp_sepSends [-1] send_cnt==0 [-1] ?show_scrollbar [-1] [0]
	
    ; Fix MacOS retina scaling for slot margins
    set tcp.fxparm.margin  	os_type==1 scale>=1.5 + [. . .]
	set tcp.sendlist.margin	os_type==1 scale>=1.5 + [. . .]
    set tcp.fxlist.margin   os_type==1 scale>=1.5 + [. . .]

    ; Calculate list and embedded FX placements for main section
	set show_main_fx 	?show_side_fx       0   !has_tcp_fxlist     0   1
	set show_main_parms ?show_side_parms    0   !tcp_sep_parms      0   1 
	set show_main_sends ?show_side_sends    0   !tcp_sepSends       0   !tcp_sends_enabled 0  1
	set show_fxembed 	tcp_fxembed>0 1 0

	set show_main_parms ?default_behavior fx_parm_cnt>0 1

	set avail_w tcp.fxparmArea{w}
	set is_full 0

	set avail_w 			?show_main_fx 		- . + fx_sec_w 		list_margin
	set is_full !is_full 	?show_main_parms 	avail_w<parm_sec_w 1
	set avail_w !is_full 	?show_main_parms	- . + parm_sec_w 	list_margin
	set is_full !is_full 	?show_main_sends 	avail_w<send_sec_w 	1
	set avail_w !is_full	?show_main_sends 	- . + send_sec_w 	list_margin

	set min_fxembed_w * 50 scale
	set show_fxembed ?is_full 0 avail_w<min_fxembed_w 0
	set show_fxembed ?is_full 0 avail_w<min_fxembed_w 0
	set avail_w !show_fxembed + list_margin

	set fx_sec_w 									avail_w<0 + . avail_w !show_fxembed + . avail_w
	set parm_sec_w  !show_main_fx 					avail_w<0 + . avail_w !show_fxembed + . avail_w 
	set send_sec_w 	!show_main_fx !show_main_parms 	avail_w<0 + . avail_w !show_fxembed + . avail_w 
    
    set main_r + tcp.fxparmArea{x} tcp.fxparmArea{w}
	set main_x tcp.fxparmArea{x}

    ; Limit main section height to a multiple of fx height
    ; This avoids stretched fx slots on MacOS and helps align scrollbars
	set fx_rows  / tcp.fxparmArea{h} fx_h
    floor fx_rows
    set fx_rows floor_result<1 1 floor_result

	set main_list_h + * fx_rows fx_h scrollbar_size

    ; Place main section lists and embedded FX
	set tcp.fxparm 		?show_main_fx [main_x tcp.fxparmArea fx_sec_w{0} main_list_h{0}]
	set main_x			?show_main_fx + + tcp.fxparm{w} list_margin

	set add_r			+ main_x parm_sec_w
	set tcp.fxlist 		?show_main_parms add_r<=main_r [main_x tcp.fxparmArea parm_sec_w{0} main_list_h{0}]
	set main_x			?show_main_parms + + tcp.fxlist{w} list_margin

	set add_r			+ main_x send_sec_w
	set tcp.sendlist 	?show_main_sends add_r<=main_r [main_x tcp.fxparmArea send_sec_w{0} main_list_h{0}]
	set main_x			?show_main_sends + + tcp.sendlist{w} list_margin

	set tcp.fxembed 	!show_fxembed [0] + [main_x tcp.fxparmArea avail_w{0} tcp.fxparmArea] * scale 4@h

    ; Reset scrollbar for main section lists
    set scrollbar_gap   scale==2 3 scale=1.5 2 1
    set scrollbar_size  * scale 4

	set tcp.fxlist.font     ?show_main_fx       [. . . . . . scrollbar_gap{0} scrollbar_size{0}]
	set tcp.fxparm.font     ?show_main_parms    [. . . . . . scrollbar_gap{0} scrollbar_size{0}]
	set tcp.sendlist.font   ?show_main_sends    [. . . . . . scrollbar_gap{0} scrollbar_size{0}]

    ; Hide elements when not enough height
	set tcp.fxparm		?supercollapsed [0] !show_side_fx{0} 		tcp.fxparmArea{h}<fx_h [0]
	set tcp.fxlist 		?supercollapsed [0] !show_side_parms{0} 	tcp.fxparmArea{h}<fx_h [0]
	set tcp.sendlist 	?supercollapsed [0] !show_side_sends{0} 	tcp.fxparmArea{h}<fx_h [0]
	set tcp.fxembed		?supercollapsed [0]                         tcp.fxparmArea{h}<fx_h [0]

	set tcp.io		                    ?supercollapsed [0]
    set tcp.fx		                    ?supercollapsed [0]
    set tcp.fxbyp                       ?supercollapsed [0]
    set tcp.custom.track_io_darker      ?supercollapsed [0]
    set tcp.custom.track_io_text_on     ?supercollapsed [0]
    set tcp.custom.track_io_text_off    ?supercollapsed [0]

    ; Swap fx and fxparm list placements when necessary
	set swap_tmp tcp.fxparm
	set swap_tmp.font tcp.fxparm.font
	set swap_tmp.margin tcp.fxparm.margin
	set needs_swap ?default_behavior 0 ?show_side_sec 1 tcp.fxlist{w}>0 1 !tcp_sepSends 0 tcp.sendlist{w}==0 0 1

	set tcp.fxparm 			!default_behavior ?tcp_sep_parms ?needs_swap tcp.fxlist
	set tcp.fxparm 			?default_behavior tcp.fxlist 
	set tcp.fxparm.font 	!tcp_sep_parms tcp.fxlist.font
	set tcp.fxparm.margin 	!tcp_sep_parms tcp.fxlist.margin

	set tcp.fxlist 			?tcp_sep_parms swap_tmp
	set tcp.fxlist.font 	!tcp_sep_parms swap_tmp.font
	set tcp.fxlist.margin 	!tcp_sep_parms swap_tmp.margin

	set tcp.fxparm !default_behavior !needs_swap !show_fxembed tcp.fxparmArea
	set tcp.fxparm ?default_behavior !show_fxembed !show_main_sends tcp.fxparmArea 
	set tcp.fxlist !needs_swap [0]
	set tcp.fxlist ?default_behavior [0]

    ; Set fxparm visibility flags based on usage
	set tcp.fxparm.visflags [1 0 0]
	set tcp.fxparm.visflags ?default_behavior [0 0 0]
	set tcp.fxparm.visflags ?tcp_sep_parms tcp.fxlist{w}>0 	+ [0 -1 0]
	set tcp.fxparm.visflags ?tcp_sepSends 	tcp.sendlist{w}>0 	+ [0 0 -1]

	;*** FTC MOD end ***
endMacro



front                                       tcp.custom.bg_solid tcp.custom.bgsel_solid
front                                       tcp.custom.bg_dimmed_1 tcp.custom.bg_dimmed_2 tcp.custom.bg_dimmed_3 tcp.custom.bg_dimmed_4 tcp.custom.bg_dimmed_5
front                                       tcp.custom.bgsel_dimmed_1 tcp.custom.bgsel_dimmed_2 tcp.custom.bgsel_dimmed_3 tcp.custom.bgsel_dimmed_4 tcp.custom.bgsel_dimmed_5
front                                       tcp.label tcp.trackidx 
front                          				tcp.fxparm tcp.fxlist tcp.sendlist tcp.fxembed tcp.recarm tcp.volume tcp.volume.label tcp.pan.label tcp.width.label tcp.solo tcp.mute tcp.phase tcp.pan tcp.width tcp.recmode tcp.foldercomp tcp.folder tcp.meter tcp.io tcp.fx tcp.fxbyp tcp.env tcp.fxin tcp.recinput 
paramPair 	tcp_indent 						2 0 3 5 9 18 36 54 ;reARKMOD //Align TCP indent correctly with the skin images
paramPair 	tcp_control_align 				79 0 1 2
set tcpLabelAutoMeasured 					param19

paramPair 	tcp_MeterSize 					4 4 12 23 39 99 179 319 ;reARKMOD //Improve TCP meter readability by making the space between both meters 1 pixel instead of 2 pixels
paramPair 	tcp_MeterLoc 					17 0 1 2
paramPair 	tcp_VolPair 					3 20 40 70 100 130 160 190 ;FTC MOD change name to VolPair
paramPair 	tcp_InPair 						20 49 64 80 100 130 190 240 ;FTC MOD change name to InPair
paramPair 	tcp_LabelPair 					18 0 20 50 80 110 140 170
paramPair 	tcp_sepSends					81 0 1

resetHides
doHideLogic 	tcp.recarm 		5
doHideLogic 	tcp.recmon 		6
doHideLogic 	tcp.label 		7
doHideLogic 	tcp.volume 		8
doHideLogic 	tcp.io 			9
doHideLogic 	fx_group 		10
doHideLogic 	tcp.env 		11
doHideLogic 	pan_group 		12
doHideLogic 	tcp.recmode 	13
doHideLogic 	input_group 	14
doHideLogic 	values 			15
doHideLogic 	meter_values	16

; ------------------------------

macro calcTcpFlow
    set any_row_r 0
    set first_row_r 0

    ;    element        	size  	padding?    fill    exclude
    then tcp.recarm 		20 			0       0       0           
    then tcp.recmon 		15 			0       0       hide_folder_recmon
    then tcp.label 	   tcp_LabelSize 	0       0       0
    then tcp.volume     tcp_VolSize 	0       0       0
    then pan_group		tcp_PanSize		1       0       0
    then tcp.io 			28 			1       0       show_side_io_button
    then fx_group 			36 			1       0       show_side_fx_button
    then tcp.env 			20 			1       0       0
    then tcp.recmode 		20 			1       0       0
    then input_group 	tcp_InSize		1       1       0
endMacro

set scale 1
calcFontSizes scale
calcTcpSecs scale
calcTcpFlow
drawTcp scale

; ------------------------------

Layout "A"
	set tcp.label .

	Layout "A_Solid" "alt" 
		set tcp.label .
	EndLayout

	Layout "150%_A" "150"
		set scale 1.5
        calcFontSizes scale
		calcTcpSecs scale
        calcTcpFlow
		drawTcp scale

        Layout "150%_A_Solid" "150/alt"
		    set tcp.label .
	    EndLayout
	EndLayout

	Layout "200%_A" "200"
		set scale 2
        calcFontSizes scale
		calcTcpSecs scale
        calcTcpFlow
		drawTcp scale

        Layout "200%_A_Solid" "200/alt"
            set tcp.label .
        EndLayout
	EndLayout
EndLayout

Layout "B"
	paramPair 	tcp_MeterSize 	32 4 12 23 39 99 179 319 ;reARKMOD //Improve TCP meter readability by making the space between both meters 1 pixel instead of 2 pixels
	paramPair 	tcp_MeterLoc 	45 0 1 2
	paramPair   tcp_VolPair     31 20 40 70 100 130 160 190 ;FTC MOD change name to VolPair
	paramPair   tcp_InPair      46 49 64 80 100 130 190 240 ;FTC MOD change name to InPair
	paramPair 	tcp_LabelPair 	47 0 20 50 80 110 140 170
	paramPair 	tcp_sepSends	82 0 1

	resetHides
	doHideLogic 	tcp.recarm 		33
	doHideLogic 	tcp.recmon 		34
	doHideLogic 	tcp.label 		35
	doHideLogic 	tcp.volume 		36
	doHideLogic 	tcp.io 			37
	doHideLogic 	fx_group 		38
	doHideLogic 	tcp.env 		39
	doHideLogic 	pan_group 		40
	doHideLogic 	tcp.recmode 	41
	doHideLogic 	input_group 	42
	doHideLogic 	values 			43
	doHideLogic 	meter_values	44

	set scale 1
    calcFontSizes scale
	calcTcpSecs scale
	calcTcpFlow
	drawTcp scale

    Layout "B_Solid" "alt" 
		set tcp.label .
	EndLayout

	Layout "150%_B" "150"
		set scale 1.5
        calcFontSizes scale
		calcTcpSecs scale
	    calcTcpFlow
		drawTcp scale

        Layout "150%_B_Solid" "150/alt"
            set tcp.label .
        EndLayout
	EndLayout

	Layout "200%_B" "200"
		set scale 2
        calcFontSizes scale
		calcTcpSecs scale
	    calcTcpFlow
		drawTcp scale

        Layout "200%_B_Solid" "200/alt"
            set tcp.label .
        EndLayout
	EndLayout
EndLayout

Layout "C"
	paramPair 	tcp_MeterSize   55 4 12 23 39 99 179 319 ;reARKMOD //Improve TCP meter readability by making the space between both meters 1 pixel instead of 2 pixels
	paramPair 	tcp_MeterLoc 	68 0 1 2
	paramPair 	tcp_VolPair 	54 20 40 70 100 130 160 190  ;FTC MOD change name to VolPair
	paramPair 	tcp_InPair 		69 49 64 80 100 130 190 240 ;FTC MOD change name to InPair
	paramPair 	tcp_LabelPair 	70 0 20 50 80 110 140 170
	paramPair 	tcp_sepSends	83 0 1

	resetHides
	doHideLogic 	tcp.recarm 		56
	doHideLogic 	tcp.recmon 		57
	doHideLogic 	tcp.label 		58
	doHideLogic 	tcp.volume 		59
	doHideLogic 	tcp.io 			60
	doHideLogic 	fx_group 		61
	doHideLogic 	tcp.env 		62
	doHideLogic 	pan_group 		63
	doHideLogic 	tcp.recmode 	64
	doHideLogic 	input_group 	65
	doHideLogic 	values 			66
	doHideLogic 	meter_values	67

	set scale 1
    calcFontSizes scale
	calcTcpSecs
    calcTcpFlow
	drawTcp

    Layout "C_Solid" "alt" 
        set tcp.label .
    EndLayout

	Layout "150%_C" "150"
		set scale 1.5
        calcFontSizes scale
		calcTcpSecs scale
        calcTcpFlow
		drawTcp scale

        Layout "150%_C_Solid" "150/alt"
            set tcp.label .
        EndLayout
	EndLayout

	Layout "200%_C" "200"
		set scale 2
        calcFontSizes scale
		calcTcpSecs scale
        calcTcpFlow
		drawTcp scale

        Layout "200%_C_Solid" "200/alt"
            set tcp.label .
        EndLayout
	EndLayout
EndLayout

macro drawTcpSeparator scale
    clear tcp.*
    set tcp.size [w h]
    set tcp.label               [0 0 w h]
    set tcp.label.font          header_font
    set tcp.label.margin        + * scale [6 4 6 4] [0 0 0 0 0.5]
    set tcp.label.color         ?track_selected [225 225 225] [190 190 190]
    set tcp.label.color         ?trackcolor_valid [trackcolor_r trackcolor_g trackcolor_b]
endmacro

Layout "Separator" "Separator"
    set scale 1
    calcFontSizes scale
    drawTcpSeparator scale
EndLayout

Layout "150%_Separator" "Separator"
    set scale 1.5
    calcFontSizes scale
    drawTcpSeparator scale
EndLayout

Layout "200%_Separator" "Separator"
    set scale 2
    calcFontSizes scale
    drawTcpSeparator scale
EndLayout

#<
#>------------------------- MASTER TRACK CONTROL PANEL --------------------------------


macro calcMasterTcpSecs scale
	set element_h 								* scale 20
	set tcp_MeterSize_s							* scale tcp_MeterSize
	set mTcp_PanSize							width_visible{0}==0 20 43
	set meter_flip_v							* scale [100 130 183] 	;[narrow_width flip_height flip_height(narrow)]
	set meter_flip								w>meter_flip_v{0} h<meter_flip_v{1} [1] [0] h<meter_flip_v{2} [1] [0]
	set meterRight								tcp_MeterLoc{0}
	set meter_sec								!meter_flip{0} [0] !meterRight{0} \
													* scale + [0 4 50 -1] [0 0 tcp_MeterSize_s h] \
													+ - + * scale [-33 0 33] [0 0 tcp_MeterSize_s h] [tcp_MeterSize_s] [w]
	set vol_sec									!meter_flip{0} [0] !meterRight{0} \
													+ - [meter_sec{2} h w] [0 0 meter_sec{2}] * scale [3 -20 -2 21] \
													- + [0 h w] * scale [0 -22 -3 22] [0 0 meter_sec{2}]
	set main_sec								?meter_flip{0} \
													!meterRight{0} \
														- - - [meter_sec{2} 0 w h] * scale [0 6] [0 0 meter_sec{2}] [0 0 0 vol_sec] \
														- - [0 0 w h] * scale [6 6] [0 0 meter_sec{2} vol_sec] \
													- - [0 0 w h] * scale [0 6] [0 0 0 meter_sec]
	set main_eff								main_sec{2}
	set mainCollapse_h							* scale 64
	set mainCollapse							main_sec{2}<mainCollapse_h{0} 1 0
	set tcp_padding								* scale [7 7]
	set tcpNullElement							+ + [main_sec main_sec] [tcp_padding tcp_padding] * scale [0 6 0 20]
	set previous_element 						tcpNullElement
endMacro


macro drawMasterTcp scale
	set master.tcp.size							* scale [300 100 40 56]
	set main_sec								!meter_flip{0} + [0 0 0 this_y_end{0}] * scale [0 0 0 8]  ; recalculate sections now we know this_y_end
	set vol_sec									!meter_flip{0} + [0 main_sec{3} w tcp_padding{1}] * scale [0 0 0 22] .
	set meter_sec								?mainCollapse{0} [0 0 w h] !meter_flip{0} tcp_fxparms>0 \
																									+ [0 vol_sec w 70] [0 vol_sec{3}] \
																									- - + [0 vol_sec w h] [0 vol_sec{3}] [0 0 0 main_sec] [0 0 0 vol_sec]
	set master.tcp.meter                   		- - + meter_sec * scale [5 2 -5 -5] [0 0 tcp.mute{2}] [0 0 tcp_padding{0}]
    set master.tcp.meter + 1@h ; FTC fix margin error
	  set master.tcp.meter.vu.div                    [1]
	  set master.tcp.meter.readout.color             [100 100 100 255 255 183 171 255]
	  set master.tcp.meter.scale.color.lit.bottom    [0 0 0 200 0 0 0 0]
	  set master.tcp.meter.scale.color.lit.top       [0 0 0 200 0 0 0 0]
	  set master.tcp.meter.scale.color.unlit.bottom  [255 255 255 120 0 0 0 0]
	  set master.tcp.meter.scale.color.unlit.top     [255 255 255 120 0 0 0 0]
	set master.tcp.mute               			+ + [meter_sec meter_sec] [meter_sec{2}] * scale [-25 4 21 20]
	set master.tcp.solo 						+ + * scale [0 2] master.tcp.mute [0 master.tcp.mute{3}]
	set master.tcp.label.color					tcp.label.color
	set master.tcp.label.font					header_font
    set master.tcp.label.margin                 + * scale [3] 0.5@ls
    set master.tcp.label.margin                 + tcp_label_voffs@y
	set master.tcp.volume						?mainCollapse{0} [0] + vol_sec * scale + [0 0 -1 -2] * ?meter_flip{0} [1 0 -1 0] [1 0 -1 -1] [1 0 1 3]
		set volume_fader_w						* 21 scale
		set master.tcp.volume.fadermode         master.tcp.volume{w}>volume_fader_w{0} [0] [1]
		set master.tcp.volume.color           	[0 0 0]
	set master.tcp.fx 							!master_fx_group{2} [0] + * scale [0 0 20] [master_fx_group master_fx_group 0 element_h{0}]
	set master.tcp.fxbyp 						!master_fx_group{2} [0] + + * scale [0 0 16] [master.tcp.fx master.tcp.fx 0 element_h{0}] [master.tcp.fx{2}]
	set master.tcp.pan 							!master_pan_group{2} [0 0 0 0] width_visible{0}==2 \
													* [master_pan_group master_pan_group master_pan_group master_pan_group element_h{0}] [1 1 1 0.5] \
													[master_pan_group master_pan_group element_h{0} element_h{0}] ; knobs have the same h and w
		set master.tcp.pan.fadermode         	gl_fader_swap
		set master.tcp.pan.color              	gl_pan_color
	set master.tcp.width						width_visible{0}==0 [0] !master_pan_group{2} [0 0 0 0] width_visible{0}==2 \
													+ [master.tcp.pan master.tcp.pan master.tcp.pan master.tcp.pan] [0 master.tcp.pan{3}] \
													- + [master_pan_group master_pan_group element_h{0} element_h{0}] [master_pan_group{2}] [element_h{0}]
		set master.tcp.width.fadermode        	gl_fader_swap
		set master.tcp.width.color            	gl_pan_color
	set hideValues								?mainCollapse{0} 1 ?hide_values{0} 1 . .	
	set main_val_color							?track_selected [160 160 160] [115 115 115]
	set master.tcp.volume.label					hideValues{0}==1 [0] + + * scale [0 -9 40 9] [main_sec main_sec{3}] - [tcp_padding] [0 tcp_padding]
		set master.tcp.volume.label.color     	main_val_color
		set master.tcp.volume.label.font		main_font
		set master.tcp.volume.label.margin    	[0]
        set master.tcp.volume.label.margin      + main_font_voffs@y
	set master.tcp.width.label					hideValues{0}==1 [0] main_eff{0}<122 [0] width_visible{0}==0 [0] + [main_sec master.tcp.volume.label] - + [main_eff{0}] * scale [-36 0 36 9] [tcp_padding]
		set master.tcp.width.label.margin     	+ [0 0 0 0 1] * scale [0 1]
		set master.tcp.width.label.color      	width_visible{0}!=2 main_val_color [90 130 90]
		set master.tcp.width.label.font			master.tcp.volume.label.font
	set master.tcp.pan.label					hideValues{0}==1 [0] main_eff{0}<86 [0] + [main_sec master.tcp.volume.label] - - + [main_eff{0}] * scale [-36 0 36 9] [tcp_padding] [master.tcp.width.label{2}]
		set master.tcp.pan.label.margin       	+ [0 0 0 0 1] * scale [0 1]
		set master.tcp.pan.label.color        	width_visible{0}!=2 main_val_color [130 90 90]
		set master.tcp.pan.label.font			master.tcp.volume.label.font									+ 	
	set master.tcp.fxparm  						- + + [0 meter_sec w h] [0 meter_sec{3}] [0 tcp_padding] + + [0 0 0 meter_sec{1}] [0 0 0 meter_sec{3}] [0 0 0 tcp_padding{1}] 
	set master.tcp.fxparm  						+ * scale [4 -2 -8 -4]
	set master.tcp.fxparm.font            		tcp.fxparm.font
	set master.tcp.fxparm.margin				tcp.fxparm.margin
endMacro

paramPair 	tcp_MeterSize 					4 4 10 22 40 80 160 320
paramPair 	tcp_MeterLoc 					17 0 1 2
set scale 1
calcFontSizes scale
calcMasterTcpSecs scale

resetHides
doHideLogic 	master_pan_group 	12
doHideLogic 	values 				15
doHideLogic 	meter_values		16

macro calcMasterTcpFlow
    ;    element        			size  	padding?
    then master.tcp.label 	   		50 			0
    then master.tcp.io 				28 			1
    then master_fx_group 			36 			1
    then master.tcp.env 			20 			1
    then master_pan_group		mTcp_PanSize	1
    then master.tcp.mono			50			1
endMacro

calcMasterTcpFlow
drawMasterTcp 1

Layout "A"
	set master.tcp.label .
EndLayout

Layout "150%_A" "150"
	set scale 1.5
	calcFontSizes scale
	calcMasterTcpSecs scale
	calcMasterTcpFlow
	drawMasterTcp scale
EndLayout

Layout "200%_A" "200"
	set scale 2
	calcFontSizes scale
	calcMasterTcpSecs scale
	calcMasterTcpFlow
	drawMasterTcp scale
EndLayout

#<
#> -------------------------- ENVELOPE CONTROL PANELS ---------------------------------

paramPair 	envcp_LabelPair 				21 0 20 50 80 110 140 170
paramPair 	envcp_VolPair 					23 20 40 70 100 130 160 190
set EnvLabelAutoMeasured 					param22
set EnvFolderIndent							param24

clear envcp.*
front		envcp.fader

macro shrinkEnvLabelAndVolume min_label_len min_vol_len max_shrink_total

	set max_shrink_label - envcp_LabelSize min_label_len
	set max_shrink_label max_shrink_label<0 0

	set max_shrink_vol - envcp_Volsize min_vol_len
	set max_shrink_vol max_shrink_vol<0 0

    ; Calculate the ratio of max shrink values
    set shrink_ratio / max_shrink_label max_shrink_vol
    
    ; Calculate initial proportional shrink values
    set shrink_vol / max_shrink_total + shrink_ratio 1
    set shrink_label * shrink_vol shrink_ratio
    
    ; Check if either volume or label exceeds its max shrink
	set shrink_label shrink_label>max_shrink_label max_shrink_label
	set shrink_vol shrink_label>max_shrink_label - max_shrink_total max_shrink_label

	set shrink_label shrink_vol>max_shrink_vol - max_shrink_total max_shrink_vol
	set shrink_vol shrink_vol>max_shrink_vol max_shrink_vol

    set did_shrink 0
	set did_shrink max_shrink_total>0 shrink_label<=max_shrink_label shrink_vol<=max_shrink_vol 1
	set did_shrink !tcp_allow_shrink 0

	set envcp_LabelSize 	?did_shrink - . shrink_label
	set envcp_VolSize  	    ?did_shrink - . shrink_vol
endmacro

macro calcEnvSecs scale
	set envcp.size								* scale [300 100]
	set element_h 								* scale 20
	set envcp_padding							* scale [6 10]
	set values_w								* scale 60
	set envcp_squash_height 					+ + * scale 20 envcp_padding{1} envcp_padding{1}
	set valuesFlipH								+ + envcp_squash_height * scale 13 envcp_padding{1}
	set h_less_border							- h 1
	set envcp_padding							h_less_border{0}<envcp_squash_height{0} + [envcp_padding] * [0 1] / - h_less_border{0} element_h * scale 2
	set envLabelMin								* scale 30
	set envcp_LabelSize 						envcp_LabelPair{0}==0 EnvLabelAutoMeasured{0}<envLabelMin{0} envLabelMin{0} + EnvLabelAutoMeasured{0} * scale 10 envcp_LabelPair
	set this_envcp_indent						+ ?EnvFolderIndent{0} * * scale tcp_indent{0} folderdepth{0} [0] * scale [-3 0 -6] ; subtracting 3px to match the lack of visible borders
	set main_sec								- + * [1 0 -1] this_envcp_indent [0 0 w h] h<valuesFlipH{0} [0 0 values_w 0] + * scale [0 0 0 13] [0 0 0 envcp_padding{1}]
    set main_sec + * scale [24 0 -30]
	set values_sec								h<valuesFlipH{0} + + [main_sec envcp_padding{1} values_w{0}] [main_sec{2}] * scale [0 4 0 13] + [0 main_sec{3} w] * scale [0 0 0 13]
	set envcpNullElement						+ + [main_sec] [envcp_padding envcp_padding] * scale [0 0 0 20]
	set previous_element 						envcpNullElement		; Let's start at the very beginning. A very good place to start.
	set addCont									envcp_type==4 [30 1] [0 0]

    set envcp_VolSize                           envcp_VolPair

    set vol_end     + envcp_padding{0} * scale + + + envcp_padding{0} envcp_LabelSize envcp_VolSize 35
    set hide_w      + envcp_padding{0} * scale 36

    ; Shrink label and volume until hide button has to be hidden
	set shrink_w / - + vol_end hide_w main_sec{w} scale

	shrinkEnvLabelAndVolume 80 40 shrink_w

    ; Shrink label and volume until both have to be hidden
	set shrink_w / - vol_end main_sec{w} scale
    set shrink_w ?did_shrink 0

	shrinkEnvLabelAndVolume 30 40 shrink_w

    set envcp_LabelSize ?tcp_allow_shrink !did_shrink shrink_w>0 0
    set envcp_VolSize   ?tcp_allow_shrink !did_shrink shrink_w>0 0

endMacro

macro envThen element width use_padding
	; apply scale
	set s_width				* width scale

	set prev_end 			+ [previous_element] [previous_element{2}]									; find the end of the previous element
	set element 			[prev_end previous_element s_width element_h]									; ...and place the element there

	; x-axis
	set element 			?use_padding + [envcp_padding] 												; if x-padding is required, add it
	set this_end			- + + [element] [element{2}] [envcp_padding] [main_sec]						; okay, and what does that make the x-end of this element, realtive to main_sec?

	; y-axis
	set next_row			+ [0 element] [0 previous_element{3}] 										; find the y of the next row
	set element 			this_end{0}>main_sec{2} + [main_sec next_row element element] envcp_padding	; move it to the next row
	set this_end			- + + [element] [element{2}] [envcp_padding] [main_sec]						; recalculate this_end, now that the element is on a new row
	set element				this_end{0}>main_sec{2} [. . 0 .]											; ... and cull if there's still not enough main_sec width to fit
	set this_y_end			+ + element{1} element{3} envcp_padding{1}									; find its bottom
	set element				this_y_end{0}>main_sec{3} [0] !previous_element{1} [0] .					; cull if there's not enough height to fit, or if previous_element was culled
	set main_sec_last 		element{3}>0 this_y_end{0} .

	; values readout control
	set valuesRoom			- - main_sec{3} this_y_end{0} envcp_padding{1}								; is the remaining height after this element enough for the values to fit?
	set hideValues			valuesRoom{0}<8 element{3}!=0 [1] . 										; if not, and this element isn't already hidden, cull the values

	set previous_element element
endmacro

macro calcEnvFlow
    ;    element        		size  	padding?  
    envThen envcp.arm  			20 			0     
    envThen envcp.bypass  		15 			0     
    envThen envcp.label   envcp_LabelSize 	0     
    envThen envcp.fader    envcp_VolSize 	0     
    envThen envcp.hide 			36 			1     
    envThen envcp.mod 		addCont{0}  addCont{1}
    envThen envcp.learn		addCont{0}  addCont{1}
endMacro

macro drawEnvcp scale
	set envcp.label.color      					?env_selected [202 202 202] [184 184 184]
		set envcp.label.font					main_font
		set envcp.label.margin     				* scale [4 0 4 0 0]
		set envcp.label.margin					+ main_font_voffs@y
	set envcp.value								+ values_sec [0 0 0 4]
		set envcp.value.font       				main_font
		set envcp.value.color      				?env_selected [84 196 255] [70 186 255]
		set envcp.value.margin     				* [0 0 scale 0 1] [0 0 7 0 1]
		set envcp.value.margin					+ main_font_voffs@y
	set envcp.fader.color      					[70 186 255]
		set env_fader_w							* 21 scale
		set envcp.fader.fadermode         		envcp.fader{w}>env_fader_w{0} [0] [1]
endMacro

set scale 1
calcFontSizes scale
calcEnvSecs scale
calcEnvFlow
drawEnvcp

Layout "A"
	set envcp.label .
EndLayout

Layout "150%_A" "150"
	set scale 1.5
	calcFontSizes scale
	calcEnvSecs scale
	calcEnvFlow
	drawEnvcp scale
EndLayout

Layout "200%_A" "200"
	set scale 2
	calcFontSizes scale
	calcEnvSecs scale
	calcEnvFlow
	drawEnvcp scale
EndLayout

#<
#>---------------------------------- THE MIXER ----------------------------------------

  ; --------------- User adjustable values. Edit without fear. -----------------

set hide_inputFX				[400] ; height below which input effects is hidden.
set hide_pan_labels          	[320] ; height below which pan labels are hidden.
set hide_pan					[260] ; height below which pan controls are hidden.
set hide_volume_label        	[200] ; height below which volume label is hidden.

  ; ------- End of user adjustable values. From here on may be dragons. ---------

macro doModeLogic variable paramIdx
	set variable param##paramIdx##&1 ?track_selected 1 0
	set variable param##paramIdx##&2 !track_selected 1 variable
	set variable param##paramIdx##&4 ?recarm 1 variable
	set variable param##paramIdx##&8 !recarm 1 variable
endmacro

macro drawMcp scale

	set hide_inputFX_s 							* scale hide_inputFX
	set hide_input_s 							* scale hide_input
	set hide_pan_labels_s 						* scale hide_pan_labels
	set hide_pan_s 								* scale hide_pan
	set hide_volume_label_s 					* scale hide_volume_label
	set extmixer_w_s 							* scale extmixer_w

	set mcp_indent_s							* scale mcp_indent
	set mcp_meterExp_s 							* scale mcp_meterExp

	clear mcp.*
	front                                   	mcp.label mcp.volume.label mcp.volume mcp.recarm mcp.recmon mcp.mute mcp.solo mcp.env mcp.io mcp.phase mcp.width mcp.trackidx mcp.folder
	set mcp_meterExp_s 							?meterExpMode{0} . 1
	set panLabelsMode 							h<hide_pan_labels_s{x} 0 labelsMode

	set gapmode									mcp_border{0}<1 0 \
													mcp_border{0}==1 1 \
													mcp_border{0}==2 2 \
													mcp_border{0}==3 folderstate==1 folderdepth==0 1 0 folderstate<0 folderdepth==1 2 0 0 \
													folderstate==1 1 folderstate<0 2 0
    ; FTC MOD Change gap_size to 11                                                
    set gap_size + 11 folderstate<0 0 1

	set nchan_offs 								?meterExpMode{0} * - tracknch 2 mcp_meterExp_s [0]
	set nchan_offs_half 						/ nchan_offs 2
	set nchan_offs_third 						/ nchan_offs 3
	set nchan_offs_twothird 					* nchan_offs_third 2
	set mcp.size                           		+ + \
													+ 	?narrowMode{0} \
															* scale [54 371] \
															* scale [86 371] \
														mcp_meterExp_s{0}>0 [nchan_offs{0}] [0] \
														+ 	gapmode{0}>=1 * scale [gap_size] [0] \
													!sidebarMode{0} [0] [extmixer_w_s] \
													* [0 0 0 scale{0}] + 180 * mcp_maxfolderdepth mcp_indent_s ; set the min height
	
    set sidebar_sec								+ + [0 0 0 0] gapmode{0}>=1 gapmode{0}!=2 * scale [0 0 gap_size{0}] [0] [0] !sidebarMode{0} [0] [0 0 extmixer_w_s{0}]
	set fx_sec								 	+ [sidebar_sec{2} 0 mcp.size{0}] * scale [0 0 0 33]
	set pan_sec 						     	+ [sidebar_sec{2} fx_sec{3} mcp.size{0}] * scale !panLabelsMode{0} h<hide_pan_s{0} [0 0 0 6] [0 0 0 28] [0 0 0 48]
	set in_sec 						         	+ + [sidebar_sec{2} pan_sec{1}] [0 pan_sec{3} mcp.size{0}] * scale h<hide_input_s{0} [0 0 0 22] h<hide_inputFX_s{0} [0 0 0 42] [0 0 0 54]
	set bot_sec_height						 	?mcp_control_align{0} \
													+ * scale 47 * mcp_maxfolderdepth mcp_indent_s \
													+ * scale 47 * folderdepth mcp_indent_s
	set bot_sec								 	- [0 h mcp.size{0} bot_sec_height{0}] [0 bot_sec_height{0}]
	set stretch_sec_start					 	+ + fx_sec{3} pan_sec{3} in_sec{3}
	set stretch_sec_height					 	- - - - h fx_sec{3} pan_sec{3} in_sec{3} bot_sec{3}
	set stretch_sec 						 	[sidebar_sec{2} stretch_sec_start{0} mcp.size{0} stretch_sec_height{0}]
	set sidebar_sec								+ + + sidebar_sec [0 0 0 stretch_sec{1}] [0 0 0 stretch_sec{3}] [0 0 0 1]
	set padding_reduction_h						* scale [300 200]
	set padding 							 	* scale stretch_sec_height{0}<padding_reduction_h{0} stretch_sec_height{0}<padding_reduction_h{1} [0 2] [0 3] [0 4]

    set mcp.extmixer.mode        				!sidebarMode{0} [-1] [1]
    set extmix_margin * scale mcp_extmix_offs
    
	set mcp.extmixer.position					!sidebarMode{0} extmix_margin@3
	set mcp.extmixer.position					?sidebarMode{0} + + sidebar_sec + [1 0 1] * scale [0 4 0 -4] gapmode{0}>=1 gapmode{0}!=2 * scale * - [gap_size 0 gap_size] [2 0 2] [1 0 -1] [0] [0]

    ; Fix MacOS retina scaling for slot margins
    set mcp.extmixer.position                   !os_type==1 + . scale==1.5 [-1 0 0 0] scale==2 [-2 0 2 0] [0]
	
    set row_h * scale mcp_fx_h
    set scrollbar_size                          scale==1  [1 4] scale==1.5  [2 6]  [2 8]
    set line_spacing                            mcp_fx_h>=34 0 1000

    set mcp.fxlist.font                         [list_font row_h{0} 0 0 line_spacing{0} 0 scrollbar_size{0} scrollbar_size{1}] 
    set mcp.sendlist.font                       [list_font row_h{0} 0 0 line_spacing{0} 0 scrollbar_size{0} scrollbar_size{1}]
    set mcp.fxparm.font                         [list_font row_h{0} 0 0 line_spacing{0} 0 scrollbar_size{0} scrollbar_size{1}] 
      
	set mcp.fxlist.margin                	    + * scale [5 0 4 0 0 14] 0.5@ls
	set mcp.sendlist.margin              	    + * scale [6 0 8 0 0 0 14] scale==1.5 0.5@x 0
	set mcp.fxparm.margin                	    * scale [3 0 4 0 0 0 14]

    ; Fix MacOS retina scaling for slot text margins
    set mcp.fxlist.margin                       os_type==1 scale>=1.5 + [. . .]
    set mcp.sendlist.margin                     os_type==1 scale>=1.5 + [. . .]
    set mcp.fxparm.margin                       os_type==1 scale>=1.5 + [. . .]
		
    set mcp.fx                               	+ [fx_sec 0 nchan_offs_twothird{0}] * scale ?narrowMode{0} [4 7 28 20] [7 7 43 20]
	set mcp.fxbyp                            	+ + [mcp.fx mcp.fx nchan_offs_third{0}] [mcp.fx{2}] * scale ?narrowMode{0} [0 0 19 20] [0 0 29 20]

	set mcp.pan   							 	?narrowMode{0} \
													h<hide_pan_s{0} [0] + [pan_sec pan_sec] 	width_visible{0}==2 	+ [0 0 nchan_offs{0}] * scale !panLabelsMode{0} [3 6 48 9] [3 15 48 9] \
																								width_visible{0}!=0		+ [nchan_offs_third{0}] * scale !panLabelsMode{0} [28 4 20 20] [26 7 20 20] \
																													+ [nchan_offs_half{0}] * scale !panLabelsMode{0} [18 4 20 20] [18 7 20 20] \
													h<hide_pan_s{0} [0] + [pan_sec pan_sec] 	width_visible{0}==2 	+ [0 0 nchan_offs{0}] * scale !panLabelsMode{0} [7 6 73 9] [7 15 73 9] \
																							width_visible{0}!=0		+ [nchan_offs_third{0}] * scale !panLabelsMode{0} [20 4 20 20] [20 7 20 20] \
																							+ [nchan_offs_half{0}] * scale !panLabelsMode{0} [33 4 20 20] [33 7 20 20]
	  set mcp.pan.fadermode                  	gl_fader_swap
	  set mcp.pan.color                      	gl_pan_color
	  set mcp.pan.label                      	?narrowMode{0} \
													!panLabelsMode{0} [0] + [pan_sec mcp.pan] width_visible{0}!=0 width_visible{0}==2 + [nchan_offs_half{0}] * scale [11 -11 35 9] + [nchan_offs_half{0}] h<hide_input_s{0} [0] * scale [17 25 35 10] + [nchan_offs_half{0}] * scale [10 25 35 10] \
													!panLabelsMode{0} [0] + [pan_sec mcp.pan] width_visible{0}!=0 width_visible{0}==2 + [nchan_offs_half{0}] * scale [23 -11 40 9] + [nchan_offs_third{0}] * scale [8 25 35 10] + [nchan_offs_half{0}] * scale [26 25 35 10]
	  set mcp.pan.label.color					val_color
	  set mcp.pan.label.font					main_font
	  set mcp.pan.label.margin               	[0 0 0 0 0.5]
      set mcp.pan.label.margin                  + main_font_voffs@y
	set mcp.width  							 	?narrowMode{0} \
													h<hide_pan_s{0} [0] + [pan_sec pan_sec] 	width_visible{0}==2 	+ [0 0 nchan_offs{0}] * scale !panLabelsMode{0} [3 14 48 9] [3 23 48 9] \
																								width_visible{0}!=0		+ [nchan_offs_third{0}] + [nchan_offs_third{0}] * scale !panLabelsMode{0} [7 20 20 20] h<hide_input{0} [9 31 20 20] [9 46 20 20] [0] \
													h<hide_pan_s{0} [0] + [pan_sec pan_sec] 	width_visible{0}==2 	+ [0 0 nchan_offs{0}] * scale !panLabelsMode{0} [7 14 73 9] [7 23 73 9] \
																								width_visible{0}!=0		+ [nchan_offs_third{0}] + [nchan_offs_third{0}] * scale !panLabelsMode{0} [48 4 20 20] [48 7 20 20] [0]
	  set mcp.width.fadermode                	gl_fader_swap
	  set mcp.width.color                    	gl_width_color
	  set mcp.width.label                    	?narrowMode{0} \
													!panLabelsMode{0} [0] + [0 mcp.width] width_visible{0}!=0 width_visible{0}==2 + [mcp.pan.label] * scale [0 11 35 9] h<hide_inputFX_s{0} [0] * scale [3 25 35 9] [0] \
													!panLabelsMode{0} [0] + [0 mcp.width] width_visible{0}!=0 width_visible{0}==2 + [mcp.pan.label] * scale [0 11 40 9] + [nchan_offs_third{0}] + * scale [-5 25 35 10] [mcp.width] [0]
	  set mcp.width.label.color					val_color
	  set mcp.width.label.font					main_font
	  set mcp.width.label.margin             	[0 0 0 0 0.5]
	  set mcp.width.label.margin             	+ main_font_voffs@y
	set mcp.recinput                         	h<hide_input_s{0} [0] + [in_sec in_sec nchan_offs{0}] * scale ?narrowMode{0} width_visible{0}==1 [0] [6 0 43 16] [6 0 75 16]
	  set mcp.recinput.color                 	?track_selected [225 225 225] [200 200 200]
	  set mcp.recinput.font						main_font
	  set mcp.recinput.margin                	+ * scale [4 0 15 0 0] scale>1 [0 0.5] [0]
	  set mcp.recinput.margin             	    + main_font_voffs@y
	set mcp.fxin                             	?narrowMode{0} [0] h<hide_inputFX_s{0} [0] + [mcp.recinput mcp.recinput] + [nchan_offs{0}] * scale [0 16 75 12]
	set mcp.recmode                          	?narrowMode{0} [0] + + [in_sec in_sec nchan_offs{0}] [0 mcp.fxin{3}] * scale + [6 4 41 16] mcp.recinput{3}>0 [0 16] [0 -4]
	set mcp.meter                            	?narrowMode{0} + * scale [4 4 22 -31] [stretch_sec stretch_sec nchan_offs{0} stretch_sec] \
													 + [stretch_sec stretch_sec nchan_offs{0} stretch_sec] * scale h<hide_volume_label_s{0} [4 4 22 -4] !labelsMode{0} [4 4 22 -4] [4 23 22 -24]
	  set mcp.meter.vu.div                   	[1 1]
	  set mcp.meter.readout.color            	!labelsMode{0} [0 0 0 0 0 0 0 0] [100 100 100 255 255 183 171 255]
	  set mcp.meter.scale.color.lit.bottom   	!labelsMode{0} [0 0 0 0 0 0 0 0] [0 0 0 170 0 0 0 0]
	  set mcp.meter.scale.color.lit.top      	!labelsMode{0} [0 0 0 0 0 0 0 0] [0 0 0 170 0 0 0 0]
	  set mcp.meter.scale.color.unlit.bottom 	!labelsMode{0} [0 0 0 0 0 0 0 0] ?recarm [255 64 0 255 0 0 0 0] [255 255 255 60 0 0 0 0]
	  set mcp.meter.scale.color.unlit.top    	!labelsMode{0} [0 0 0 0 0 0 0 0] ?recarm [255 64 0 255 0 0 0 0] [255 255 255 60 0 0 0 0]
    set mcp.meter !narrowMode{0} + * scale -1@h ; FTC MOD clean up meter size going into trackidx
	set mcp.volume.label                     	?narrowMode{0} [0] \
													+ [stretch_sec stretch_sec mcp.meter{2}] * scale h<hide_volume_label_s{0} [1 0 28 4] !labelsMode{0} [1 0 28 4] [1 0 28 23]
	  set mcp.volume                         	?narrowMode{0} + - + [stretch_sec stretch_sec] [nchan_offs_half stretch_sec{3}] * scale [-4 21] * scale [0 0 21 21] \
													- + * [1 1 scale 1] [mcp.meter stretch_sec 21 stretch_sec] [mcp.meter{2} mcp.volume.label{3}] [-2 0 0 mcp.volume.label{3}]
	  set mcp.volume.fadermode 					!narrowMode{0} -1 .
	  set mcp.volume.label.color             	mcp.volume.label{h}>2 [100 100 100] [38 38 38]
	  set mcp.volume.label.font					main_font
	  set mcp.volume.label.margin            	* [scale scale scale scale 1] ?narrowMode{0} [0 80 0 0 0.5] [0 4 0 0 0.5]
	  set mcp.volume.label.margin          	    + main_font_voffs@y
      set mcp.volume.label.margin               h<hide_volume_label_s{0} + * scale [0 4] ; FTC Mod fix glitch on Windows (label remains partly visible when it should be hidden)
    set mcp.volume.label scale==1.5 + -1@x ; MOD (moved from layout) ---- scaling error cleanup ----
	set mcp.recarm                           	+ * scale [0 0 36 24] ?narrowMode{0} - + [mcp.meter{2}] [stretch_sec stretch_sec] * scale [-1 17] \
														+ - [stretch_sec stretch_sec] * scale [-27 17] [mcp.meter{2}]
	set mcp.recmon                           	+ + [0 padding] [mcp.recarm mcp.recarm] * scale [7 20 21 20]
	set mcp.mute                             	+ + [0 padding] [mcp.recmon mcp.recmon] * scale [0 19 21 20]
	set mcp.solo                             	+ + [0 padding] [mcp.mute mcp.mute] * scale [0 21 21 20]
	set mcp_io_hide_h							* scale 106
	set mcp.io                               	stretch_sec_height{0}<mcp_io_hide_h{0} [0] + + [0 padding] [mcp.solo mcp.solo] * scale !labelsMode{0} [-1 23 23 30] [-1 23 23 30]
	set mcp_env_hide_h							* scale [125 141]
	set mcpHideEnv								!labelsMode{0} stretch_sec_height{0}<mcp_env_hide_h{0} 1 0 stretch_sec_height{0}<mcp_env_hide_h{1} 1 0
	set mcp.env                              	?mcpHideEnv{0} [0 0 0 0] + [0 stretch_sec{3}] + [mcp.io stretch_sec] * scale !labelsMode{0} [1 -22 21 22] [1 -30 21 30]
	set mcp_phase_hide_h						* scale [144 162]
	set mcpHidePhase							!labelsMode{0} stretch_sec_height{0}<mcp_phase_hide_h{0} 1 0 stretch_sec_height{0}<mcp_phase_hide_h{1} 1 0
	set mcp.phase                            	?mcpHidePhase{0} [0] + [mcp.env mcp.env] - * scale [3 -18 16 18] padding
	set mcp.label 							 	+ + + * scale \
													gapmode{0}==1 - * [1 0 -1 1] [gap_size{0} 0 gap_size{0} 27] ?sidebarMode{0} [2 0 -2]  [0] \
													gapmode{0}==2 - * [1 0 -1 1] [0           0 gap_size{0} 27] ?narrowMode{0}  [0 0 -2] [0] \
													[0 0 0 26] \
													[0 bot_sec mcp.size{0}] \
													?mcp_control_align{0} * - mcp_maxfolderdepth folderdepth [0 0 0 mcp_indent_s{0}] [0]
	  set mcp.label.font						header_font
	  set mcp.label.margin                   	* [scale scale scale scale 1] [4 0 4 0 0.5]
	   set mcp.label.margin						+ 1.5@y ; FTC MOD
	  trackLabelColor 							mcp.label.color
	set mcp.trackidx                         	+ + [0 bot_sec] [0 mcp.label{3}] - [0 0 mcp.size{0} bot_sec_height{0}] [0 0 0 mcp.label{3}]
	  set mcp.trackidx.margin                	- [0 1 0 mcp.trackidx{3} 0.5] * scale [0 0 0 20]
	  	set mcp.trackidx.margin 					folderstate==1 + * scale ?narrowMode [4] [8]
	  text_over_custom 							mcp.trackidx.color
	  set mcp.trackidx.color					?track_selected [24 24 24] .
	  set mcp.trackidx.font                  	main_font
	;set mcp.folder                           	+ + [0 bot_sec] [0 mcp.label{3}] folderstate<0 + [mcp.size] * scale [-21 -2 21 22] * scale [0 -2 18 22]

	;b0se synkrotron folder end tweak, replacing above line
		set folder_width	* / 1 scale mcp.size{0} ; b0se synkrotron folder end tweak, replacing above line
		set mcp.folder		+ + [0 bot_sec] [0 mcp.label{3}] folderstate<0 + + [mcp.size] [sidebar_sec{2}] * scale [-21 -2 21 22] * scale [0 -2 folder_width 22]

    ; FTC Hide folder dot image when indent > 0 (by increasing size and moving it out of view)
    set mcp.folder folderstate==1 param1{0}>1 + * scale 30@w

    ;*** FTC MOD add custom folder indent images for MCP    
	front mcp.custom.folder_1-1 mcp.custom.folder_1-2 mcp.custom.folder_1-4 mcp.custom.folder_1-8 mcp.folder mcp.custom.folder_start  

	set custom_folder scale==1.0 - + [w mcp.folder{y}] [0 0 16 21] [16 -1]
	set custom_folder scale==1.5 - + [w mcp.folder{y}] [0 0 25 33] [25 0]
	set custom_folder scale==2.0 - + [w mcp.folder{y}] [0 0 32 42] [32 -2]
	
	set mcp.custom.folder_1-1 			folderstate==1 param1{0}>=5 custom_folder [0]
	set mcp.custom.folder_1-2 			folderstate==1 param1{0}==4 custom_folder [0]
	set mcp.custom.folder_1-4 			folderstate==1 param1{0}==3 custom_folder [0]
	set mcp.custom.folder_1-8 			folderstate==1 param1{0}==2 custom_folder [0]

	set custom_folder_start  [mcp.folder custom_folder custom_folder custom_folder]
	set mcp.custom.folder_start			!folderstate==1 [0] !folderdepth==0 param1{0}==1 custom_folder_start [0] custom_folder_start
    ;*************
endMacro

macro DrawStrip scale
    clear mcp.volume

    set mcp.recinput	[0]
    set mcp.recmode		[0]
    set mcp.fxin		[0]

    set mcp.size		+ * [scale] [-52]

    ; Reposition elements
    set mcp.label		+ * [scale scale scale] [0 0 -52]
    set mcp.trackidx	+ * [scale scale scale] [0 0 -52]
		set mcp.trackidx.margin folderstate==1 + * scale [7]

    set mcp.folder		!folderstate=1 + * [scale scale] [-52 1]	

	set mcp.custom.folder_1-1 [0]
	set mcp.custom.folder_1-2 [0]
	set mcp.custom.folder_1-4 [0]
	set mcp.custom.folder_1-8 [0]

    set mcp.fx		    + * [scale scale scale] [-6 0 -22]
    set mcp.fxbyp		+ * [scale scale scale] [-28 0 -17]

    set mcp.mute		+ * [scale] [-46]
    set mcp.solo		+ * [scale] [-46]
    set mcp.io		    + * [scale] [-46]
    set mcp.phase		+ * [scale] [-46]
    set mcp.env		    + * [scale] [-46]

    set mcp.meter		    + * [scale scale scale scale] [0 -1 -17.5 1.5]
    set mcp.volume.label	+ * [scale scale scale scale] [0 0 -17 1]
    
    set mcp.volume.label.margin	* [scale scale scale scale 1] [0 3 0 0 0.5]
    set mcp.volume.label.margin	!labelsMode{0} * [scale] [300]	

    set mcp.recarm		+ * [scale] [-49]
    set mcp.recmon		+ * [scale scale] [-49 -43]

    set mcp.pan		    + * [scale] [-26]
    set mcp.pan.label	+ * [scale] [-26]

    set mcp.width		+ * [scale scale] [0 1]
    set mcp.width.label	+ * [scale scale] [-26 1]

    set mcp.pan         trackpanmode==5 + * [scale scale] [13 0]
    set mcp.pan.label	trackpanmode==5 + * [scale scale] [18 0]
    set mcp.width	    trackpanmode==5 + * [scale scale] [-41 39] 
    set mcp.width.label	trackpanmode==5 + * [scale scale] [-17 38]
    set mcp.recmon	    trackpanmode==5 !recarm [0]
    set mcp.width	    trackpanmode==5 ?recarm [0]
    set mcp.width.label	trackpanmode==5 ?recarm [0]

    set mcp.pan		    trackpanmode==6 + * [scale scale scale] [21 0 -42]
    set mcp.width       trackpanmode==6 + * [scale scale scale] [-5 0 -42]
    set mcp.pan.label	trackpanmode==6 + * [scale scale scale] [4 0 -8]

    ; Get height of area between rec button and FX button
    set pan_area - mcp.recarm{y} + mcp.fx{y} mcp.fx{h}
    set pan_h0  * [scale] 20
    set pan_h1  * [scale] 40
    set pan_h2  * [scale] 75
    set pan_h3  * [scale] 90

    ; Move elements based on height
    set mcp.width	trackpanmode==5 pan_area{0}<pan_h3{0} + * [scale scale] [0 -10]
    set mcp.width	trackpanmode==5 pan_area{0}<pan_h2{0} + * [scale scale] [0 -6]
    set mcp.pan 	trackpanmode==6 pan_area{0}<pan_h2{0} + * [scale scale] [0 -9]
    set mcp.width	trackpanmode==6 pan_area{0}<pan_h2{0} + * [scale scale] [0 -9]
    set mcp.pan 	trackpanmode==6 pan_area{0}<pan_h1{0} + * [scale scale] [0 8]
    set mcp.width	trackpanmode==6 pan_area{0}<pan_h1{0} + * [scale scale] [0 8]
    set mcp.pan 	trackpanmode==6 pan_area{0}>pan_h1{0} !labelsMode{0} + * [scale scale] [0 8]
    set mcp.width	trackpanmode==6 pan_area{0}>pan_h1{0} !labelsMode{0} + * [scale scale] [0 8]

    ; Hide elements based on height
    set mcp.recmon	pan_area{0}<pan_h0{0} [0]
    set mcp.recmon  pan_area{0}<pan_h1{0} !recarm [0]
    set mcp.recmon  trackpanmode==5 pan_area{0}<pan_h2{0} !recarm [0]

    set mcp.pan         pan_area{0}<pan_h1{0} ?recarm [0]

    set mcp.pan.label   pan_area{0}<pan_h2{0} [0]
    set mcp.pan.label	trackpanmode==5 pan_area{0}<pan_h3{0} [0]

    set mcp.width	trackpanmode<=5 pan_area{0}<pan_h1{0} [0]
    set mcp.width   trackpanmode<=5 pan_area{0}<pan_h2{0} ?recarm [0]
    set mcp.width	trackpanmode==6 pan_area{0}<pan_h1{0} ?recarm [0]

    set mcp.width.label pan_area{0}<pan_h2{0} [0]
    set mcp.width.label trackpanmode<6 pan_area{0}<pan_h3{0} [0]

    ; Get height of area between label and route button
    set io_b + mcp.io{y} mcp.io{h}
    set fader_area  - mcp.label{y} io_b{0}

    set vol_knob_size * scale 20
    set min_vol_knob_h + vol_knob_size * scale 5

    set fader_env_h   * scale 125
    set fader_phase_h * scale 180

    set mcp.env fader_area{0}<fader_env_h{0} [0]
    set mcp.phase fader_area{0}<fader_phase_h{0} [0]

    ; Give fader the remaining height
    set vol_margin_t * [scale] 3
    set vol_margin_b * [scale] mcp.phase{w}>0 3 1
    set small_fader_h * scale 62
    set vol_margin_t fader_area{0}<small_fader_h{0} + * scale 2
    set vol_margin_b fader_area{0}<small_fader_h{0} + * scale 1
    set vol_t + + mcp.io{y} mcp.io{h} vol_margin_t{0}
    set vol_b - mcp.phase{h}>0 mcp.phase{y} mcp.env{h}>0 mcp.env{y} mcp.label{y} vol_margin_b{0}
    set vol_h - vol_b{0} vol_t{0}
   
    set mcp.volume [mcp.io{x} vol_t{0} mcp.io{w} vol_h{0}]
    
    set min_vol_h * scale 45
    set min_vol_x + mcp.io{x} * [scale] [2]
    set min_vol_margin_b mcp.env{w}>0 3 0
    set min_vol_y - - vol_b{0} vol_knob_size{0} * scale min_vol_margin_b{0}
    
    set mcp.volume.fadermode vol_h{0}<min_vol_h{0} [1] [0]
    set mcp.volume vol_h{0}<min_vol_h{0} [min_vol_x{0} min_vol_y{0} vol_knob_size{0} vol_knob_size{0}]

    set mcp.io vol_h{0}<min_vol_knob_h{0} mcp.env{w}==0 [0]

endMacro

set hide_inputFX				[400] 	; height below which input effects is hidden.
set hide_input              	[350] 	; height below which input dropdown is hidden.
set hide_pan_labels          	[320] 	; height below which pan labels are hidden.
set hide_pan					[260] 	; height below which pan controls are hidden.
set hide_volume_label        	[250] 	; height below which volume label is hidden.
set extmixer_w					[86] 	; width of the extmixer when set as a sidebar

  ; sidebar (pushes other secs right)
  ; fx section (background is baked into mcp_bg)
  ; pan section
  ; input section (show/hide inputFX on resize / recarm, with user override)
  ; stretch section (recarm pokes above)
  ; bottom section (name resize on maxfolderdepth)

paramPair mcp_indent 			1 0 5 10 15 20 25 30
paramPair mcp_control_align 	78 0 1

doModeLogic sidebarMode 	25
doModeLogic narrowMode 		26
doModeLogic meterExpMode 	27
doModeLogic labelsMode 		28
paramPair mcp_meterExp 		29 0 2 4 8
paramPair mcp_border 		30 0 1 2 3 4

set scale 1
calcFontSizes scale
drawMcp scale

Layout "A"
	set mcp.label .

	Layout "A_Fader_Red" "red"
		set mcp.label .
	EndLayout
		
	Layout "A_Fader_Orange" "orange"
		set mcp.label .
	EndLayout
		
	Layout "A_Fader_Yellow" "yellow"
		set mcp.label .
	EndLayout
		
	Layout "A_Fader_Green" "green"
		set mcp.label .
	EndLayout

	Layout "A_Fader_Teal" "teal"
		set mcp.label .
	EndLayout
		
	Layout "A_Fader_Blue" "blue"
        set mcp.label .
    EndLayout
		
	Layout "A_Fader_Purple" "purple"
		set mcp.label .
	EndLayout
		
	Layout "A_Fader_Pink" "pink"
		set mcp.label .
	EndLayout

	Layout "A_Fader_White" "white"
		set mcp.label .
	EndLayout

	Layout "150%_A" "150"
        set scale 1.5
        calcFontSizes scale
		drawMcp scale

        Layout "150%_A_Fader_Red" "150/red"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Orange" "150/orange"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Yellow" "150/yellow"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Green" "150/green"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Teal" "150/teal"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Blue" "150/blue"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Purple" "150/purple"
            set mcp.label .
        EndLayout
        
        Layout "150%_A_Fader_Pink" "150/pink"
            set mcp.label .
        EndLayout

        Layout "150%_A_Fader_White" "150/white"
            set mcp.label .
        EndLayout
	EndLayout
	
	Layout "200%_A" "200"
        set scale 2
		calcFontSizes scale
		drawMcp scale

        Layout "200%_A_Fader_Red" "200/red"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Orange" "200/orange"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Yellow" "200/yellow"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Green" "200/green"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Teal" "200/teal"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Blue" "200/blue"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Purple" "200/purple"
            set mcp.label .
        EndLayout
        
        Layout "200%_A_Fader_Pink" "200/pink"
            set mcp.label .
        EndLayout

        Layout "200%_A_Fader_White" "200/white"
            set mcp.label .
        EndLayout
	EndLayout

    Layout "A_Strip" "strip"
        set narrowMode 0
        set scale 1
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout
        
    Layout "150%_A_Strip" "150/strip"
        set narrowMode 0
        set scale 1.5
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout    

    Layout "200%_A_Strip" "200/strip"
        set narrowMode 0
        set scale 2
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout
EndLayout

Layout "B"
	set sidebarMode 			0
	set narrowMode 				0
	set meterExpMode 			0
	set labelsMode 				0
	doModeLogic sidebarMode 	48
	doModeLogic narrowMode 		49
	doModeLogic meterExpMode 	50
	doModeLogic labelsMode 		51
	paramPair mcp_meterExp 		52 0 2 4 8
	paramPair mcp_border 		53 0 1 2 3 4
    
    set scale 1
    calcFontSizes scale
	drawMcp scale

	Layout "150%_B" "150"
        set scale 1.5
        calcFontSizes scale
        drawMcp scale
	EndLayout

	Layout "200%_B" "200"
        set scale 2
        calcFontSizes scale
        drawMcp scale
	EndLayout

    Layout "B_Strip" "strip"
        set narrowMode 0
        set scale 1
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout
        
    Layout "150%_B_Strip" "150/strip"
        set narrowMode 0
        set scale 1.5
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout

    Layout "200%_B_Strip" "200/strip"
        set narrowMode 0
        set scale 2
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout
EndLayout

Layout "C"
	set sidebarMode 			0
	set narrowMode 				0
	set meterExpMode 			0
	set labelsMode 				0
	doModeLogic sidebarMode 	71
	doModeLogic narrowMode 		72
	doModeLogic meterExpMode 	73
	doModeLogic labelsMode 		74
	paramPair mcp_meterExp 		75 0 2 4 8
	paramPair mcp_border 		76 0 1 2 3 4
    
    set scale 1
    calcFontSizes scale
	drawMcp scale

	Layout "150%_C" "150"
        set scale 1.5
        calcFontSizes scale
        drawMcp scale
	EndLayout

	Layout "200%_C" "200"
        set scale 2
        calcFontSizes scale
        drawMcp scale
	EndLayout

    Layout "C_Strip" "strip"
        set narrowMode 0
        set scale 1
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout
        
    Layout "150%_C_Strip" "150/strip"
        set narrowMode 0
        set scale 1.5
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout    

    Layout "200%_C_Strip" "200/strip"
        set narrowMode 0
        set scale 2
        calcFontSizes scale
        drawMcp scale
        drawStrip scale
    EndLayout
EndLayout

; ====== Start OF MCP SEPARATOR LAYOUTS ====== : (thank you LCS!)

macro drawMcpSeparator scale
    clear mcp.*
    set mcp.size	            + [0 h] * scale [mcp_separator_w]
    set mcp.label               [0 0 w h]
    set mcp.label.font          header_font
    set mcp.label.margin        + * scale [4 6 4 6] [0 0 0 0 0.5]
    set mcp.label.color         ?track_selected [225 225 225] [190 190 190]
    set mcp.label.color         ?trackcolor_valid [trackcolor_r trackcolor_g trackcolor_b]
    set mcp.extmixer.mode       [1]
    set mcp.extmixer.position   [0]
endMacro

Layout "Separator" "separator"
    set scale 1
    calcFontSizes scale
    drawMcpSeparator scale
EndLayout

Layout "150%_Separator" "separator"
    set scale 1.5
    calcFontSizes scale
    drawMcpSeparator scale
EndLayout

Layout "200%_Separator" "separator"
    set scale 2
    calcFontSizes scale
    drawMcpSeparator scale
EndLayout

;== END OF SEPARATORS

#<
#>--------------------------------- MASTER MIXER --------------------------------------

macro drawMasterMcp scale

	clear master.mcp.*
	front                							master.mcp.menubutton master.mcp.io master.mcp.env master.mcp.mute master.mcp.solo
	set master.mcp.size         					+ * scale [125 370] [0 0 0 mcp.size]
	set hide_pan_sm 								* scale hide_pan
	set hide_pan_labels_sm							* scale hide_pan_labels
	set fx_sec								 		+ [0 0 master.mcp.size{0}] * scale [0 0 0 27]
	set pan_sec 						     		+ [0 fx_sec{3} master.mcp.size{0}] * scale h<hide_pan_labels_sm{0} h<hide_pan_sm{0} [0 0 0 6] [0 0 0 28] [0 0 0 48]
	set bot_sec								 		+ [0 h master.mcp.size{0}] * scale [0 -23 0 23]
	set stretch_sec_start					 		+ fx_sec{3} pan_sec{3}
	set stretch_sec_height					 		- - - h fx_sec{3} pan_sec{3} bot_sec{3}
	set stretch_sec 						 		[0 stretch_sec_start{0} master.mcp.size{0} stretch_sec_height{0}]
	set padding_reduction_h							* scale [300 200]
	set padding 							 		* scale stretch_sec_height{0}<padding_reduction_h{0} stretch_sec_height{0}<padding_reduction_h{1} [0 2] [0 3] [0 4]
	set master.mcp.fx                               * scale [7 7 82 20]
	set master.mcp.fxbyp                            * scale [89 7 29 20]
	set master.mcp.pan   							+ [0 pan_sec] * scale h<hide_pan_sm{0} [0]  	width_visible{0}==2 	h<hide_pan_labels_sm{0} [7 5 67 9] [7 15 67 9] \
																								width_visible{0}!=0		h<hide_pan_labels_sm{0} [20 4 20 20] [20 7 20 20] \
																													h<hide_pan_labels_sm{0} [31 4 20 20] [31 7 20 20]
	  set master.mcp.pan.fadermode              	gl_fader_swap
	  set master.mcp.pan.color                  	gl_pan_color
	  set master.mcp.pan.label                  	h<hide_pan_labels_sm{x} [0] + [0 master.mcp.pan] * scale width_visible{0}!=0 width_visible{0}==2 [23 -11 35 9] [6 25 35 10] [23 25 35 10]
	  set master.mcp.pan.label.color				[140 140 140]
	  set master.mcp.pan.label.font					main_font
	  set master.mcp.pan.label.margin           	[0 0 0 0 0.5]
	  set master.mcp.pan.label.margin           	+ main_font_voffs@y
	set master.mcp.width  							+ [0 pan_sec] * scale h<hide_pan_sm{0} [0]  	width_visible{0}==2 	h<hide_pan_labels_sm{0} [7 13 67 9] [7 23 67 9] \
																								width_visible{0}!=0		h<hide_pan_labels_sm{0} [48 4 20 20] [48 7 20 20] [0]
	  set master.mcp.width.fadermode            	gl_fader_swap
	  set master.mcp.width.color                	gl_width_color
	  set master.mcp.width.label                	h<hide_pan_labels_sm{x} [0] + [0 master.mcp.width] * scale width_visible{0}!=0 width_visible{0}==2 [23 11 35 9] [41 25 35 10] [0]
	  set master.mcp.width.label.color				[140 140 140]
	  set master.mcp.width.label.font				main_font
	  set master.mcp.width.label.margin         	[0 0 0 0 0.5]
	  set master.mcp.width.label.margin           	+ main_font_voffs@y
	set master.mcp.mono                             + [0 pan_sec] * scale h<hide_pan_labels_sm{0} h<hide_pan_sm{0} [0] [93 4 25 22] [93 8 25 33]
	set master.mcp.volume.label                     h<hide_volume_label{0} [0 0 0 0] + [0 stretch_sec] * scale [1 0 89 23]
	  set master.mcp.volume                         - + + * scale [69 0 21] [0 stretch_sec 0 stretch_sec] [0 master.mcp.volume.label{3}] [0 0 0 master.mcp.volume.label{3}]
	  set master.mcp.volume.label.color             master.mcp.volume.label{h}>2 [100 100 100] [38 38 38]
	  set master.mcp.volume.label.font				main_font
	  set master.mcp.volume.label.margin            + [0 0 0 0 0.5] * scale [0 4]
	  set master.mcp.volume.label.margin           	+ main_font_voffs@y
	set master.mcp.meter                            + [0 master.mcp.volume 0 master.mcp.volume] * scale [4 0 59 -6]
	  set master.mcp.meter.vu.div                   [1]
	  set master.mcp.meter.vu.rmsdiv                [2]
	  set master.mcp.meter.readout.color            [255 255 255 200 0 0 0 0]
	  set master.mcp.meter.rmsreadout.color         [217 217 217 217 0 0 0 0]
	  set master.mcp.meter.scale.color.lit.bottom   [0 0 0 200 0 0 0 0]
	  set master.mcp.meter.scale.color.lit.top      [0 0 0 200 0 0 0 0]
	  set master.mcp.meter.scale.color.unlit.bottom [255 255 255 120 0 0 0 0]
	  set master.mcp.meter.scale.color.unlit.top    [255 255 255 120 0 0 0 0]
	set master.mcp.label                            bot_sec
	  set master.mcp.label.color                    [184 184 184]
	  set master.mcp.label.font						header_font
	  set master.mcp.label.margin                   + * scale [20 0] [0 0 0 0 0.5]
	  set master.mcp.label.margin					+ 0.5@y ; FTC MOD
	set master.mcp.mute                             + [0 stretch_sec] * scale [95 6 21 20]
	set master.mcp.solo                             + + [0 padding] [master.mcp.mute master.mcp.mute] * scale [0 21 21 20]
	set master_mcp_env_hide_h						* scale 141
	set master.mcp.env                              stretch_sec_height{0}<master_mcp_env_hide_h{0} [0] + + [0 stretch_sec] [0 stretch_sec{3}] * scale [95 -37 21 30]
	set master_mcp_io_hide_h						* scale 106
	set master.mcp.io                               stretch_sec_height{0}<master_mcp_io_hide_h{0} [0] + + [0 padding] [master.mcp.solo master.mcp.solo] * scale [0 23 23 30]
	set master.mcp.menubutton                       + * scale [0 2 20 20] [bot_sec bot_sec]
	
    ; FTC MOD Hide master track icon
    set master.mcp.extmixer.mode                    [-1]
    set master.mcp.extmixer.position                !master_mcp_hide_icon  [0]  * [0 -1 0 0] mcp_iconsize
    set master.mcp.margin                           !master_mcp_hide_icon  .    * [0 -1 0 0]  mcp_iconsize
    ; *** end of mod ***

    ; FTC MOD extmixer offset
    set extmix_margin * scale master_mcp_extmix_offs
	set master.mcp.extmixer.position					+ extmix_margin@3
    ; *** end of mod ***

    ; Fix MacOS retina scaling for slot margins
    set master.mcp.extmixer.position                   !os_type==1 + . scale==1.5 [-1 0 0 0] scale==2 [-2 0 2 0] [0]

    set row_h * scale mcp_fx_h
    set scrollbar_size                          scale==1  [1 4] scale==1.5  [2 6]  [2 8]
    set line_spacing                            mcp_fx_h>=34 0 1000 

    set master.mcp.fxlist.font                         [list_font row_h{0} 0 0 line_spacing{0} 0 scrollbar_size{0} scrollbar_size{1}] 
    set master.mcp.sendlist.font                       [list_font row_h{0} 0 0 line_spacing{0} 0 scrollbar_size{0} scrollbar_size{1}]
    set master.mcp.fxparm.font                         [list_font row_h{0} 0 0 line_spacing{0} 0 scrollbar_size{0} scrollbar_size{1}] 
      	
	set master.mcp.fxlist.margin                	+ * scale [5 0 4 0 0 14] 0.5@ls
	set master.mcp.sendlist.margin              	* scale [6 0 8 0 0 0 14]
	set master.mcp.fxparm.margin                	* scale [3 0 4 0 0 14]

    ; Fix MacOS retina scaling for slot text margins
    set master.mcp.fxlist.margin                    os_type==1 scale>=1.5 + [. . .]
    set master.mcp.sendlist.margin                  os_type==1 scale>=1.5 + [. . .]
    set master.mcp.fxparm.margin                    os_type==1 scale>=1.5 + [. . .]
endmacro

set scale 1
calcFontSizes scale
drawMasterMcp scale

Layout "A"
    set master.mcp.label .
EndLayout

Layout "150%_A" "150"
    set scale 1.5
    calcFontSizes scale
    drawMasterMcp scale
EndLayout

Layout "200%_A" "200"
    set scale 2
    calcFontSizes scale
    drawMasterMcp scale
EndLayout

#<
#>---------------------------------- TRANSPORT ----------------------------------------

;----- tune by eye @1x -----
set status_min	 			130		; the size below which status readout looks weird
set selection_min 			180		; the size below which selection readout looks weird
;---------------------------
set text_color             	[194 198 206] 	; the color of the text


;returns [padding x, padding y, width, height, width + padding x]
macro size element   px  py  w  h
  set size_##element * scale + [px py w h px] [0 0 0 0 w]
endmacro

macro drawTransport scale

	clear trans.*
	set trans.size             		* scale [100 100]
	paramPair 	trans_rateSize		77 72 130 160 200 250 310 380
	set trans_rateSize				* scale trans_rateSize{0}
	set trans_rateSize_padded		+ trans_rateSize{0} * scale 3
	set size_rate 					+ * scale [3 0 0 24] [0 0 trans_rateSize{0} 0 trans_rateSize_padded{0}]

	; Set absolute sizes...
	;     element   px  py  w   h
	size  rew       4   8   28  30
	size  fwd       0   8   28  30
	size  rec       10  6   26  28
	size  play      6   6   26  28
	size  repeat    6   6   26  28
	size  stop      10	8   28	30
	size  pause     0   8   28	30
	size  automode  3   10   50  18
	size  status    24  6   370 26
	size  bpm       0   7   50  24
	size  timesig   0   0   40  36
	size  selection 0   10   300 20

	set row_height      			* scale [36]
	set row_height_x2            	* 2 row_height
	set row_height_x3            	* 3 row_height
	set row_height_x4            	* 4 row_height

	set scaleMargin					+ * scale [1 1 1 1] [0 0 0 0 1]

	set showRate 					?trans_flags&2 1 0
	set size_rate                	!showRate{0} [0 0 0 0 0] .
	set size_timesig             	?trans_flags&4 . [0 0 0 0 0]

	; size the sections
	set sec_buttons_width           + + + + + + size_rew{4} size_fwd{4} size_stop{4} size_play{4} size_pause{4} size_rec{4} size_repeat{4}
	set sec_buttons                 [0 0 sec_buttons_width{0}]
	set sec_proj_width           	+ + + size_bpm{4} size_timesig{4} size_rate{4} size_automode{4}
	set sec_proj                 	[0 0 sec_proj_width{0} row_height{0}]
	set all_width                	+ + + sec_buttons_width size_status{4} sec_proj_width size_selection{4}
	set buttons_proj_width			+ sec_buttons_width sec_proj_width
	set buttons_proj_cutoff			- buttons_proj_width{0} w

	set w_stretch					- all_width w
	set sec_status_width			- h<row_height_x2{0} w<all_width{0} - size_status{4} * w_stretch 0.6 size_status{4} size_status{4} size_status{0}
	set sec_sel_width				h<row_height_x2{0} w<all_width{0} - size_selection{4} * w_stretch 0.4 size_selection{4} size_selection{4}
	set sec_sel						[0 0 sec_sel_width{0}]

	; layout the sections
	set sec_status					[sec_buttons{2} 0 sec_status_width{0} size_status]
	set sec_status					sec_sel{2}<selection_min{0} + sec_status [0 0 sec_sel] ; proj section is going to be hidden, so steal its width
	set sec_status					sec_status_width{0}<status_min{0} [0] .
	set sec_proj					- [w 0 sec_proj{2} sec_proj] [sec_proj{2}]
	set sec_proj					h<row_height_x2{0} w<buttons_proj_width{0} [0] . . ; single row, hide proj section if it still doesn't fit
	set sec_sel						sec_sel{2}<selection_min{0} [0] - - [w size_selection sec_sel size_selection] [sec_proj{2}] [sec_sel{2}]

		; jumping rows when panel is narrow
		; ...2 rows
		set sec_sel		            w<all_width{0} h<row_height_x2{0} . + [0 row_height{0}] [0 sec_sel sec_sel sec_sel] .
		set sec_proj	            w<all_width{0} h<row_height_x2{0} . + [sec_sel row_height{0}] [sec_sel{2} 0 sec_proj sec_proj] .
		; ...4 rows
		set row_1_width				+ sec_buttons_width sec_status_width
		set row_2_width				+ sec_proj_width sec_sel_width
		set 2rows_max				row_1_width{0}>row_2_width{0} row_1_width{0} row_2_width{0}
		set sec_status_width		w<sec_status_width{0} - w size_status{0}
		set sec_status				w<2rows_max{0} [0 row_height{0} sec_status_width{0} size_status] .
		set sec_sel					w<sec_sel{2} [sec_sel sec_sel w sec_sel]
		set sec_sel					w<2rows_max{0} + [0 row_height_x2{0}] [size_selection size_selection sec_sel size_selection] .
		set sec_proj				w<sec_proj{2} [sec_proj sec_proj w sec_proj]
		set sec_proj				w<2rows_max{0} [0 row_height_x3{0} sec_proj sec_proj] .

		set trans_min_width			h>row_height{0} sec_status_width{0}<sec_proj_width{0} sec_proj_width{0} sec_status_width{0} + * scale [216] [0 row_height{0}]
		set trans.size.dockedheight	row_height{0}
		set trans.size.minmax		[trans_min_width row_height{0} 10000 row_height_x4{0}]

	; transport center
	set transCenter 			!sec_sel{2} 0 w<all_width{0} h>row_height{0} 0 ?trans_flags&1 1 0 ?trans_flags&1 1 0
	set sec_buttons_center      * 0.5 - w +  + + sec_buttons_width{0} sec_status_width{0} sec_proj_width{0} sec_sel_width{0}
	set sec_buttons             ?transCenter{0} + sec_sel_width{0}>0 + [sec_sel_width{0}] [sec_buttons_center{0}] [0] [0 0 sec_buttons] .
	set sec_status              ?transCenter{0} + [sec_buttons 0 sec_status size_status] [sec_buttons{2}] .
	set sec_sel					?transCenter{0} [0 size_selection sec_sel size_selection] .

	; draw content in sec_buttons
	set trans.rew                	+ [sec_buttons sec_buttons] [size_rew size_rew size_rew size_rew]
	set trans.fwd                	+ + [trans.rew sec_buttons] [size_rew{2}] [size_fwd size_fwd size_fwd size_fwd]
	set trans.rec                	+ + [trans.fwd sec_buttons] [size_fwd{2}] [size_rec size_rec size_rec size_rec]
	set trans.play               	+ + [trans.rec sec_buttons] [size_rec{2}] [size_play size_play size_play size_play]
	set trans.repeat             	+ + [trans.play sec_buttons] [size_play{2}] [size_repeat size_repeat size_repeat size_repeat]
	set trans.stop               	+ + [trans.repeat sec_buttons] [size_repeat{2}] [size_stop size_stop size_stop size_stop]
	set trans.pause              	+ + [trans.stop sec_buttons] [size_stop{2}] [size_pause size_pause size_pause size_pause]

	; draw content in sec_status
	set trans.status             	+ [sec_status sec_status sec_status sec_status] [size_status size_status]

	; draw content in sec_proj
	set trans_bpm                	+ [sec_proj sec_proj] [size_bpm size_bpm size_bpm size_bpm]
	  set trans.bpm.tap          	sec_proj{2}==0 [0] + * [1 1 1 0.5] trans_bpm * scale -1@y
	  set trans.bpm.edit         	sec_proj{2}==0 [0] + + trans.bpm.tap [0 trans.bpm.tap{h}] * scale 1@y
	set trans.curtimesig         	sec_proj{2}==0 [0] + + [trans_bpm sec_proj] [size_bpm{2}] [size_timesig size_timesig size_timesig size_timesig]
    set trans.curtimesig            + * scale [0 6 0 -9] ; FTC MOD
      set trans.curtimesig.color 	[. . . . 43 43 43 255]
	set trans.automode			 	sec_proj{2}==0 [0] + + [trans.curtimesig sec_proj] [size_timesig{2}] [size_automode size_automode size_automode size_automode]
	set trans_rate               	+ + [trans.automode sec_proj] [size_automode{2}] [size_rate size_rate size_rate size_rate]
    
      set rate_cutoff				- + trans_rate{0} trans_rate{2} w
	  set trans_rate				rate_cutoff{0}>0 - trans_rate [0 0 rate_cutoff{0}] .
	  set rate_mode_change			* scale 100
	  set trans_rate_mode			size_rate{2}<rate_mode_change{0} 1 0
	  set trans.rate             	sec_proj{2}==0 [0] !showRate{0} [0 0 0 0] ?trans_rate_mode{0} + [trans_rate trans_rate] * scale [30 1 42 36] * [1 1 1 0.5] trans_rate
	  set trans.rate.fader       	sec_proj{2}==0 [0] !showRate{0} [0 0 0 0] ?trans_rate_mode{0} + [trans_rate trans_rate] * scale [3 3 30 30] + trans.rate [0 trans.rate{h}]
	  set trans.rate.fader.fadermode	?trans_rate_mode{0} 1 0
	  set trans.rate.fader.color 	[255 255 255 100]
	  set trans.rate.font           main_font
	  set trans.rate.fader          + * scale 2@h ; FTC MOD
	  set trans.rate.fader          ?trans_rate_mode{0} + . * scale 1@y + . * scale [39 0 -70] ; FTC MOD
      set trans.rate                ?trans_rate_mode{0} + . * scale [0 2 0 -6] + + trans.rate.fader trans.rate.fader{w}@w * scale [-32 0 72 0] ; FTC MOD
      front trans.rate.fader

	; draw content in sec_sel
	set trans.sel					sec_sel

	set trans.margin				* scale [12 0 12 0]
	set trans.curtimesig.color 		+ text_color [0 0 0 0  0 0 0]
	set trans.status.margin    		* scale [6 0 32 0]
	set trans.status.margin    		os_type==0  + . scale==1 [0 0] scale==1.5 [0 1 0 -1] scale==2 [0 1 0 1] [0]
	set trans.status.margin    		os_type==1  + . scale==1 [0 1] scale==1.5 [0 1 0 1]  scale==2 [0 1 0 1] [0]
	set trans.status.margin    		os_type==2  + . scale==1 [0 1] scale==1.5 [0 2 0 0]  scale==2 [0 1 0 1] [0]
	set trans.curtimesig.font  		main_font
	set trans.bpm.tap.font  		main_font
	set trans.bpm.edit.font  		main_font
	set trans.sel.font        	 	header_font
	set trans.bpm.tap.margin   		[0 0 0 0 0.5]
	set trans.bpm.edit.margin 	 	[0 0 0 0 0.5]
	set trans.bpm.edit.margin  		[0 0 0 0 0.5]
	set trans.sel.margin      	 	* scaleMargin [4 0 4 0 1]
	set trans.bpm.edit.color   		text_color
	set trans.bpm.tap.color   	 	text_color
	set trans.rate.color      	 	text_color
	set trans.sel.color      	  	text_color

endMacro

set scale 1
calcFontSizes scale
drawTransport scale

Layout "A"
	set trans.status .
EndLayout

Layout "150%_A" "150"
	set scale 1.5
    calcFontSizes scale
    drawTransport scale
EndLayout

Layout "200%_A" "200"
	set scale 2
    calcFontSizes scale
    drawTransport scale
EndLayout


#<
#>------------------------------- DPI TRANSLATION -------------------------------------

; ------ DUMMY LAYOUTS ---------

Layout "50%_A"
	set tcp.label  			.
	set mcp.label  			.
	set envcp.label			.
	set master.mcp.label 	.
	set master.tcp.label 	.
	set trans.status 		.
EndLayout

Layout "75%_A"
	set tcp.label  			.
	set mcp.label  			.
	set envcp.label			.
	set master.mcp.label 	.
	set master.tcp.label 	.	
	set trans.status 		.
EndLayout

Layout "50%_B"
	set tcp.label  			.
	set mcp.label  			.
EndLayout

Layout "75%_B"
	set tcp.label  			.
	set mcp.label  			.
EndLayout

Layout "50%_C"
	set tcp.label  			.
	set mcp.label  			.
EndLayout

Layout "75%_C"
	set tcp.label  			.
	set mcp.label  			.
EndLayout

;						requested	scale >	use
layout_dpi_translate 	'50%_A' 	1.0 	'A'
layout_dpi_translate 	'50%_B' 	1.0 	'B'
layout_dpi_translate 	'50%_C' 	1.0 	'C'

layout_dpi_translate 	'75%_A' 	1.0 	'A'
layout_dpi_translate 	'75%_B' 	1.0 	'B'
layout_dpi_translate 	'75%_C' 	1.0 	'C'

layout_dpi_translate 	'75%_A' 	1.74 	'150%_A'
layout_dpi_translate 	'75%_B' 	1.74 	'150%_B'
layout_dpi_translate 	'75%_C' 	1.74 	'150%_C'

layout_dpi_translate 	'A' 		1.34 	'150%_A'
layout_dpi_translate 	'B' 		1.34	'150%_B'
layout_dpi_translate 	'C' 		1.34 	'150%_C'

layout_dpi_translate 	'A' 		1.74 	'200%_A'
layout_dpi_translate 	'B' 		1.74 	'200%_B'
layout_dpi_translate 	'C' 		1.74 	'200%_C'

layout_dpi_translate 	'150%_A' 	1.34 	'200%_A'
layout_dpi_translate 	'150%_B' 	1.34 	'200%_B'
layout_dpi_translate 	'150%_C' 	1.34 	'200%_C'

; -- no layout chosen, dpi_translate as A

layout_dpi_translate 	'' 			1.34 	'150%_A'
layout_dpi_translate 	'' 			1.74 	'200%_A'

;-------------------------- REAPERTIPS Translations --------------------------

Layout "50%_A_Solid" "alt" 
	set tcp.label  			.
EndLayout

Layout "75%_A_Solid" "alt" 
	set tcp.label  			.
EndLayout

Layout "50%_B_Solid" "alt" 
	set tcp.label  			.
EndLayout

Layout "75%_B_Solid" "alt" 
	set tcp.label  			.
EndLayout

Layout "50%_C_Solid" "alt" 
	set tcp.label  			.
EndLayout

Layout "75%_C_Solid" "alt" 
	set tcp.label  			.
EndLayout

Layout "50%_A_Strip" "strip" 
	set mcp.label  			.
EndLayout

Layout "75%_A_Strip" "strip" 
	set mcp.label  			.
EndLayout

Layout "50%_B_Strip" "strip" 
	set mcp.label  			.
EndLayout

Layout "75%_B_Strip" "strip" 
	set mcp.label  			.
EndLayout

Layout "50%_C_Strip" "strip" 
	set mcp.label  			.
EndLayout

Layout "75%_C_Strip" "strip" 
	set mcp.label  			.
EndLayout

Layout "50%_Separator" "Separator" 
	set tcp.label			.
	set mcp.label  			.
EndLayout

Layout "75%_Separator" "Separator" 
	set tcp.label			.
	set mcp.label  			.
EndLayout

;						requested	        scale  > use
layout_dpi_translate 	'50%_A_Solid' 	    1.0 	'A_Solid'
layout_dpi_translate 	'50%_B_Solid' 	    1.0 	'B_Solid'
layout_dpi_translate 	'50%_C_Solid' 	    1.0 	'C_Solid'
layout_dpi_translate 	'50%_A_Strip' 		1.0 	'A_Strip'
layout_dpi_translate 	'50%_B_Strip' 		1.0 	'B_Strip'
layout_dpi_translate 	'50%_C_Strip' 		1.0 	'C_Strip'
layout_dpi_translate 	'50%_Separator' 	1.0 	'Separator'

layout_dpi_translate 	'75%_A_Solid' 	    1.0 	'A_Solid'
layout_dpi_translate 	'75%_B_Solid' 	    1.0 	'B_Solid'
layout_dpi_translate 	'75%_C_Solid' 	    1.0 	'C_Solid'
layout_dpi_translate 	'75%_A_Strip' 		1.0 	'A_Strip'
layout_dpi_translate 	'75%_B_Strip' 		1.0 	'B_Strip'
layout_dpi_translate 	'75%_C_Strip' 		1.0 	'C_Strip'
layout_dpi_translate 	'75%_Separator' 	1.0 	'Separator'

layout_dpi_translate 	'75%_A_Solid' 	    1.74 	'150%_A_Solid'
layout_dpi_translate 	'75%_B_Solid' 	    1.74 	'150%_B_Solid'
layout_dpi_translate 	'75%_C_Solid' 	    1.74 	'150%_C_Solid'
layout_dpi_translate 	'75%_A_Strip' 		1.74 	'150%_A_Strip'
layout_dpi_translate 	'75%_B_Strip' 		1.74 	'150%_B_Strip'
layout_dpi_translate 	'75%_C_Strip' 		1.74 	'150%_C_Strip'
layout_dpi_translate 	'75%_Separator' 	1.74 	'150%_Separator'

layout_dpi_translate 	'A_Solid' 		    1.34 	'150%_A_Solid'
layout_dpi_translate 	'B_Solid' 		    1.34 	'150%_B_Solid'
layout_dpi_translate 	'C_Solid' 		    1.34 	'150%_C_Solid'
layout_dpi_translate 	'A_Strip' 			1.34 	'150%_A_Strip'
layout_dpi_translate 	'B_Strip' 			1.34 	'150%_B_Strip'
layout_dpi_translate 	'C_Strip' 			1.34 	'150%_C_Strip'
layout_dpi_translate 	'Separator'	 		1.34 	'150%_Separator'

layout_dpi_translate 	'A_Solid' 		    1.74 	'200%_A_Solid'
layout_dpi_translate 	'B_Solid' 		    1.74 	'200%_B_Solid'
layout_dpi_translate 	'C_Solid' 		    1.74 	'200%_C_Solid'
layout_dpi_translate 	'A_Strip' 			1.74 	'200%_A_Strip'
layout_dpi_translate 	'B_Strip' 			1.74 	'200%_B_Strip'
layout_dpi_translate 	'C_Strip' 			1.74 	'200%_C_Strip'
layout_dpi_translate 	'Separator'	 		1.74 	'200%_Separator'

layout_dpi_translate 	'150%_A_Solid' 	    1.34 	'200%_A_Solid'
layout_dpi_translate 	'150%_B_Solid' 	    1.34 	'200%_B_Solid'
layout_dpi_translate 	'150%_C_Solid' 	    1.34 	'200%_C_Solid'
layout_dpi_translate 	'150%_A_Strip' 		1.34 	'200%_A_Strip'
layout_dpi_translate 	'150%_B_Strip' 		1.34 	'200%_B_Strip'
layout_dpi_translate 	'150%_C_Strip' 		1.34 	'200%_C_Strip'
layout_dpi_translate 	'150%_Separator'	1.34 	'200%_Separator'

;MCP Colored_Fader_Layouts

;						requested	        	scale  > use
layout_dpi_translate 	'A_Fader_Red'	 	    1.34 	'150%_A_Fader_Red'
layout_dpi_translate 	'A_Fader_Orange' 	    1.34 	'150%_A_Fader_Orange'
layout_dpi_translate 	'A_Fader_Yellow' 	    1.34 	'150%_A_Fader_Yellow'
layout_dpi_translate 	'A_Fader_Green'	        1.34 	'150%_A_Fader_Green'
layout_dpi_translate 	'A_Fader_Teal'	        1.34 	'150%_A_Fader_Teal'
layout_dpi_translate 	'A_Fader_Blue' 	        1.34 	'150%_A_Fader_Blue'
layout_dpi_translate 	'A_Fader_Purple' 	    1.34 	'150%_A_Fader_Purple'
layout_dpi_translate 	'A_Fader_Pink' 	        1.34 	'150%_A_Fader_Pink'
layout_dpi_translate 	'A_Fader_White' 	    1.34 	'150%_A_Fader_White'

layout_dpi_translate 	'A_Fader_Red'	 	    1.74 	'200%_A_Fader_Red'
layout_dpi_translate 	'A_Fader_Orange' 	    1.74 	'200%_A_Fader_Orange'
layout_dpi_translate 	'A_Fader_Yellow' 	    1.74 	'200%_A_Fader_Yellow'
layout_dpi_translate 	'A_Fader_Green'	        1.74 	'200%_A_Fader_Green'
layout_dpi_translate 	'A_Fader_Teal'	        1.74 	'200%_A_Fader_Teal'
layout_dpi_translate 	'A_Fader_Blue' 	        1.74 	'200%_A_Fader_Blue'
layout_dpi_translate 	'A_Fader_Purple' 	    1.74 	'200%_A_Fader_Purple'
layout_dpi_translate 	'A_Fader_Pink' 	        1.74 	'200%_A_Fader_Pink'
layout_dpi_translate 	'A_Fader_White' 	    1.74 	'200%_A_Fader_White'

layout_dpi_translate 	'150%_A_Fader_Red'	 	1.34 	'200%_A_Fader_Red'
layout_dpi_translate 	'150%_A_Fader_Orange' 	1.34 	'200%_A_Fader_Orange'
layout_dpi_translate 	'150%_A_Fader_Yellow' 	1.34 	'200%_A_Fader_Yellow'
layout_dpi_translate 	'150%_A_Fader_Green'	1.34 	'200%_A_Fader_Green'
layout_dpi_translate 	'150%_A_Fader_Teal'	 	1.34 	'200%_A_Fader_Teal'
layout_dpi_translate 	'150%_A_Fader_Blue' 	1.34 	'200%_A_Fader_Blue'
layout_dpi_translate 	'150%_A_Fader_Purple' 	1.34 	'200%_A_Fader_Purple'
layout_dpi_translate 	'150%_A_Fader_Pink' 	1.34 	'200%_A_Fader_Pink'
layout_dpi_translate 	'150%_A_Fader_White' 	1.34 	'200%_A_Fader_White'

#<
